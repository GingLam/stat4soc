---
date: "2021-08-01T12:05:05-05:00"
tags:
- 推断统计
title: 第十节 描述统计与多组均值检验
---


#### 0.预处理
* 设置工作目录
```
cd "/Users/ginglam/Public/data".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset name clds.
dataset activate clds.
```


#### 1.检验多个独立样本总体均值是否相等（样本组别 >= 3）
* 单因素方差分析（one-way ANOVA）
* 方差分析的三大前提假设（assumptions）
	* 抽自总体的各组随机样本相互独立，即独立性假设（independence）
	* 各组随机样本中，待检验变量的总体服从正态分布，即正态性假设（normality）
	* 各组随机样本中，待检验变量的总体具有相等的方差，即方差齐性假设（homoscedasticity）

#### 案例（1-1）：以2015年总收入为例，试检验不同季节出生的劳动者的平均收入是否相等
* 虚无假设H0：不同季节出生的劳动者在2015年拥有相同的总收入
* 研究假设H1：不同季节出生的劳动者在2015年拥有不同的总收入

* 第一步：处理收入变量和出生月份变量
	* 注意：此时不保留收入变量为0的样本
```
select if (I3a_6  > 0).
select if (birthmonth > 0).
compute income = I3a_6.
recode birthmonth (3 thru 5 = 1)(6 thru 8 = 2)(9 thru 11 = 3)(12,1,2 = 4).
value labels birthmonth 1 "春季出生" 2 "夏季出生" 3 "秋季出生" 4 "冬季出生". 
variable level birthmonth (nominal).
means income birthmonth
	/cells = min max.
```

![方差分析](https://stat4soc.netlify.app/images/10.1.png)

* 利用均值表呈现不同行业的收入情况
```
means income by birthmonth
	/cells = mean count var stddev.
```

![方差分析](https://stat4soc.netlify.app/images/10.2.png)

* 条形图（调用高级绘图功能，参见前面讲义内容）
```
ggraph 
	/graphdataset name = "clds" variables= birthmonth mean(income)[name = "mean_income"]
	/graphspec source = inline. 
begin gpl 
	SOURCE: clds=userSource(id("clds"))
	DATA: birthmonth=col(source(clds), name("birthmonth"), unit.category())
	DATA: mean_income=col(source(clds), name("mean_income"))
	GUIDE: axis(dim(1), label("出生季节"))
	GUIDE: axis(dim(2), label("平均收入"))
	GUIDE: text.title(label("2015年出生季节劳动者的总收入")) 
	GUIDE: text.subtitle(label("基于CLDS2016数据"))
	ELEMENT: interval(position(birthmonth*mean_income))
end gpl.
```

![方差分析](https://stat4soc.netlify.app/images/10.3.png)

#### 第二步：检查变量的正态性（normality）
* 对左偏分布的收入变量进行底数为自然数e的对数转换，使之接近正态分布
* 当收入变量为0时，无法进行对数转换，这类样本必须被排除
```
if (income > 0) log_income = ln(I3a_6).
select if (log_income  >= 0).
means log_income
	/cells = mean count var stddev min max.
examine variables = log_income
	/plot histogram.
```

![方差分析](https://stat4soc.netlify.app/images/10.4.png)

![方差分析](https://stat4soc.netlify.app/images/10.5.png)

#### 检验定距和定比变量的正态性，有三种判断方法

#### 第一种方法：根据收入对数分布的峰度和偏度来判断

* **点选操作包含两种方式**
	* 第一种方式采用“频率”功能：分析 -> 描述统计 -> 频率 -> 选择变量 -> 去除勾选“显示”频率表 -> 点击统计 -> 勾选“表示后验分布的特征”中“偏度”和“峰度” -> 继续 -> 确定
	* 第二种方式采用“描述”功能：分析 -> 描述统计 -> 描述 -> 选择变量 -> 点击统计量 ->  勾选“表示后验分布的特征”中“偏度”和“峰度” -> 继续 -> 确定
* **语法操作包含两种方式**
	* 采用“频率”功能：frequencies variables = + “变量名” + /format = + “notable”（不输出频率表） + /statistics = + “skewness”（偏度） + “seskew”（偏度标准误差） + “kurtosis”（峰度） + “sekurt”（峰度标准误差）
	* 采用“描述”功能：descriptives variables = + “变量名” + /statistics = +  “kurtosis”（峰度） + “skewness”（偏度）

#### 案例（1-1-1）：检查收入变量的正态性
* **第一种方式：采用“频率”功能**
```
frequencies variables = log_income
	/format = notable
	/statistics = skewness seskew kurtosis sekurt.
```

![方差分析](https://stat4soc.netlify.app/images/10.6.png)

* **第二种方式：采用“描述”功能**
```
descriptives variables=log_income 
	/statistics=kurtosis skewness.
```

![方差分析](https://stat4soc.netlify.app/images/10.7.png)

* 从结果可知，收入对数分布的峰度和偏度均接近于0，说明收入对数分布接近正态分布
  
#### 第二种方法：根据P-P图和Q-Q图来判断
* **P-P图**
	* **点选操作：** 分析 -> 描述统计 -> P-P图 -> 选择变量 -> 确定
	* **语法操作：** pplot + /type= + “p-p”（呈现P-P图）+ /variables = + “变量名”
* **Q-Q图**
	* **点选操作分为两种方式**
		* 第一种方式采用“探索”功能：分析 -> 描述统计 -> 探索 -> 仅填入因变量列表 -> 点击“图” -> 勾选“含检验的正态图” -> 其他取消勾选或选“无” -> 继续 -> 确定
		* 第二种方式采用“Q-Q图”功能：分析 -> 描述统计 -> Q-Q图 -> 选择变量 -> 确定
	* **语法操作分两种方式**
		* 第一种方式采用“探索”功能：examine variables = +  “变量名” + /plot + “npplot”（Q-Q图）+ /nototal（不显示其他统计结果）
		* 第二种方式采用“Q-Q图”功能：pplot + /type= + “p-p”（呈现P-P图）+ /variables = + “变量名”

#### 案例（1-1-2）：检查收入变量的正态性
* **P-P图**
```
pplot
	/type=p-p 
	/variables = log_income.
```

![方差分析](https://stat4soc.netlify.app/images/10.8.png)

![方差分析](https://stat4soc.netlify.app/images/10.9.png)

* 从结果可知，收入对数分布的观测值与正态分布的预期累积概率拟合线几乎完全重叠，说明收入对数分布接近正态分布
* **Q-Q图**
* **第一种方式采用“探索”功能**
```
examine variables=log_income 
	/plot npplot 
	/nototal.
```

![方差分析](https://stat4soc.netlify.app/images/10.10.png)

![方差分析](https://stat4soc.netlify.app/images/10.11.png)

![方差分析](https://stat4soc.netlify.app/images/10.12.png)

* **第二种方式采用“Q-Q图”功能**
```
pplot
	/type=q-q 
	/variables = log_income.
```

![方差分析](https://stat4soc.netlify.app/images/10.13.png)

![方差分析](https://stat4soc.netlify.app/images/10.14.png)

![方差分析](https://stat4soc.netlify.app/images/10.15.png)

* 从结果可知，收入对数分布的观测值与正态分布的预期正态值拟合线几乎完全重叠，说明收入对数分布接近正态分布

#### 第三种：根据柯尔莫可洛夫-斯米洛夫检验（kolmogorov-smirnov test）或夏皮罗-威尔克检验（Shapiro-Wilktest test）
* 柯尔莫可洛夫-斯米洛夫检验或夏皮罗-威尔克检验是最严格的正态性检验，两种方法对样本量均十分敏感
* 当样本量较大时，只要数据稍有偏离正态分布，P值就会小于0.05，检验结果倾向于拒绝虚无假设，即认为变量总体不服从正态分布

#### 柯尔莫可洛夫-斯米洛夫检验
* **点选操作分为两种方式**
	* 第一种方式采用“探索”功能：与QQ图“探索”功能相同
	* 第二种方式采用“非参数检验”功能：分析 -> 非参数检验 -> 旧对话框 -> 单样本K-S(1) -> 仅填入检验变量列表 -> 勾选检验分布“正态” -> 确定
* **语法操作分为两种方式**
	* 第一种方式“探索”功能：与Q-Q图“探索”功能相同
	* 第二种方式“非参数检验”功能：npar tests + k-s(normal) = “变量名”
#### 夏皮罗-威尔克检验
* **点选操作与Q-Q图“探索”功能相同**
* **语法操作与Q-Q图“探索”功能相同**

#### 案例（1-1-3）：检查收入变量的正态性
* **柯尔莫可洛夫-斯米洛夫检验**
* **第一种方式采用“探索”功能**
```
examine variables=log_income 
	/plot npplot 
	/nototal.
```

![方差分析](https://stat4soc.netlify.app/images/10.16.png)

![方差分析](https://stat4soc.netlify.app/images/10.17.png)

![方差分析](https://stat4soc.netlify.app/images/10.18.png)

* **第二种方式采用“非参数检验”功能**
```
npar tests 
	/k-s(normal)=log_income.
```

![方差分析](https://stat4soc.netlify.app/images/10.19.png)

* **夏皮罗-威尔克检验**
```
examine variables=log_income 
	/plot npplot 
	/nototal.
```

![方差分析](https://stat4soc.netlify.app/images/10.20.png)

* 两项检验结果P值均小于0.05，拒绝虚无假设（收入的总体分布为正态分布）
* 由于两项检验较为严格，根据收入对数分布的峰度、偏度、P-P图和Q-Q图，可以认为收入对数分布近似正态分布

#### 随堂练习：采用以上三种方法，试检验教育年限（I2_1）及其对数形式是否具有正态性


#### 第三步：检查变量的方差齐性（homoscedasticity）或同方差性（homogeneity of variance）
* 根据莱文检验（levene test）进行判断
* **点选操作：** 分析 -> 比较平均值 -> 单因素ANOVA检验 -> 点击“选项” -> 勾选“方差齐性检验” -> 继续 -> 确定
* **语法操作：** oneway + “因变量” + by + “分组变量” + /statistics + homogeneity（同方差）

#### 案例（1-1-4）：检查收入变量的方差齐性
* 进行莱文检验
```
oneway log_income by birthmonth 
	/statistics homogeneity.
```

![方差分析](https://stat4soc.netlify.app/images/10.21.png)

#### 随堂练习：试检验从事不同产业（I3a_8）劳动者的教育年限（I2_1）及其对数形式是否具有方差齐性.
* 提示：对I3a_8而言，“1,2” 为第一产业，“3 thru 7”为第二产业，“8 thru 16”为第三产业.


#### 第四步：进行方差分析（ANOVA）
#### 第一种方法：通过统计公式直接进行计算
* 该方法本质上是对样本进行方差分解

#### 案例（1-1-5）：试用方差分析，检验不同季节出生劳动者，其总收入是否存在差异
* 计算所有劳动者的总收入（取对数）均值
```
aggregate 
	/log_income_mean = mean(log_income).
```
* 计算所有劳动者的总收入（取对数）的总平方和（SST）.
```
compute diff_sq = (log_income - log_income_mean)**2.
aggregate 
	/SST = sum(diff_sq).
compute df_t = 555-1.
```
* 计算所有劳动者的总收入（取对数）的组间平方和（SSB）
* 计算不同出生组的平均收入.
```
means log_income by birthmonth
	/cells = mean count.
compute log_income_spring = 10.017473.
compute log_income_summer = 9.882176.
compute log_income_fall = 9.715442.
compute log_income_winter = 9.962090.
```
* 计算组间平方和.
```
compute SSB = 146*((log_income_spring-log_income_mean)**2) + 142*((log_income_summer-log_income_mean)**2) + 150*((log_income_fall-log_income_mean)**2) + 117*((log_income_winter-log_income_mean)**2).
compute df_b = 4-1.
```
* 计算所有劳动者的总收入（取对数）的组内平方和（SSW）或残差平方和（SSE）
```
if (birthmonth = 1) log_income_spring_diffsq=(log_income-log_income_spring)**2.
if (birthmonth = 2) log_income_summer_diffsq=(log_income-log_income_summer)**2.
if (birthmonth = 3) log_income_fall_diffsq=(log_income-log_income_fall)**2.
if (birthmonth = 4) log_income_winter_diffsq=(log_income-log_income_winter)**2.
aggregate 
	/log_income_spring_sum = sum(log_income_spring_diffsq).
aggregate 
	/log_income_summer_sum = sum(log_income_summer_diffsq). 
aggregate 
	/log_income_fall_sum = sum(log_income_fall_diffsq).
aggregate 
	/log_income_winter_sum = sum(log_income_winter_diffsq). 
compute SSW = log_income_spring_sum + log_income_summer_sum + log_income_fall_sum + log_income_winter_sum.
compute df_w = 555-4.
```
* 呈现方差分析结果
```
formats SST df_t SSB df_b SSW df_w (f20.16).
descriptives SST df_t SSB df_b SSW df_w
	/statistics mean.
```

![方差分析](https://stat4soc.netlify.app/images/10.22.png)

* 根据统计公式计算检验统计量
```
compute F_value = (SSB/df_b)/(SSW/df_w).
formats F_value (f20.16).
descriptives F_value
	/statistics mean.
```

![方差分析](https://stat4soc.netlify.app/images/10.23.png)

* 根据F检验结果，F值等于1.781837
* 在F分布下，P值等于整个F分布的累积密度（面积=1）与样本均值处的累积密度（面积 = cdf.f(实际F值,自由度1,自由度2)）之差
```
compute p_value_f = (1 - cdf.f(1.781837,3,551)).
formats p_value_f (f20.16).
descriptives p_value_f
	/statistics mean.
```

![方差分析](https://stat4soc.netlify.app/images/10.24.png)

* 根据F检验结果，P值等于0.149516153378015且大于0.05的显著性水平，不能拒绝虚无假设，即认为不同季节出生的劳动者在2015年可能拥有相同的总收入，也可能拥有不同的总收入


#### 第二种方法：采用“单因素ANOVA检验”功能进行计算
* **点选操作：** 分析 -> 比较平均值 -> 单因素ANOVA检验 -> 点击“选项” -> 勾选“平均值图” -> 继续 -> 确定
* **语法操作：** oneway + “因变量” + by + “分组变量” + /plot + means（呈现均值图）

#### 案例（1-1-6）：试用方差分析，检验不同季节出生劳动者，其总收入是否存在差异
* 采用“单因素ANOVA检验”功能
```
oneway log_income by birthmonth
/plot means.
```

![方差分析](https://stat4soc.netlify.app/images/10.25.png)

* 根据F检验结果，F值等于1.782，P值等于0.149517且大于0.05的显著性水平，不能拒绝虚无假设，即认为不同季节出生的劳动者在2015年可能拥有相同的总收入，也可能拥有不同的总收入.


#### 随堂练习：以2015年社会地位得分（I7_10_1）为例，试检验不同地区（东中西）劳动者的平均社会地位得分是否相等.
* 提示：首先要将省份编码（PROV2016）转为地区编码
* 第一步：省份编码参考2014年中华人民共和国行政区划代码
	* http://www.mca.gov.cn/article/sj/xzqh/1980/201507/20150715854923.shtml
	* 11 = 北京 12 = 天津 13 = 河北 14 = 山西 15 = 内蒙古 
	* 21 = 辽宁 22 = 吉林 23 = 黑龙江 31 = 上海 32 = 江苏 
	* 33 = 浙江 34 = 安徽 35 = 福建 36 = 江西 37 = 山东 
	* 41 = 河南 42 = 湖北 43 = 湖南 44 = 广东 45 = 广西 
	* 50 = 重庆 51 = 四川 52 = 贵州 53 = 云南 61 = 陕西 
	* 62 = 甘肃 63 = 青海 64 = 宁夏 65 = 新疆
* 第二步：不同地区的划分参考国家统计局“东西中部和东北地区划分方法”	
	* http://www.stats.gov.cn/ztjc/zthd/sjtjr/dejtjkfr/tjkp/201106/t20110613_71947.htm
	* 东北地区样本量过少，可以与西部地区样本合并
* 试判断变量的正态性和方差齐性，再进行方差分析


#### 2.检验多个独立样本的两项总体均值是否相等（样本组别 >= 3，自变量 = 2）
* **点选操作：** 分析 -> 一般线性模型 -> 单变量 -> 选择“因变量”填入“因变量”框 -> 选择“两个自变量”填入“固定因子”框 -> 点击“模型” -> 选择“构建项” -> 选择“类型”为“主效应” -> 将“因子与协变量”移入“模型” -> 选择“平方和”为“III类” -> 勾选“在模型中包括截距” -> 继续 -> 确定
* **语法操作：** unianova + “因变量” + by + 处理项变量 + 区集项变量 + /method = + “sstype(3)”（III类平方和）+ /intercept = + “include”（包括截距）+ /criteria = + “alpha(显著性水平)”

#### 案例（2-1）：以2015年总收入为例，试检验不同季节出生和不同性别的劳动者的平均收入是否相等
* 虚无假设H0-1:不同季节出生的劳动者在2015年拥有相同的总收入
* 虚无假设H0-2:不同性别的劳动者在2015年拥有相同的总收入
* 研究假设H1-1:不同季节出生的劳动者在2015年拥有不同的总收入
* 研究假设H1-2:不同性别的劳动者在2015年拥有不同的总收入

* 使用双因素方差分析（two-ways ANOVA）
```
select if (gender > 0).
unianova log_income by birthmonth gender
	/method = sstype(3) 
	/intercept = include 
	/criteria = alpha(0.05)
	/design = birthmonth gender.
```

![方差分析](https://stat4soc.netlify.app/images/10.26.png)

* 根据F1检验结果，F1值等于1.709387，P1值等于0.163985且大于0.05的显著性水平，不能拒绝虚无假设1，即认为不同季节出生的劳动者在2015年可能拥有相同的总收入，也可能拥有不同的总收入
* 根据F2检验结果，F2值等于22.379861，P2值等于0.000003且小于0.05的显著性水平，拒绝虚无假设2，即认为不同性别的劳动者在2015年拥有不相同的总收入

#### 随堂练习：以2015年总收入为例，试检验不同季节出生和不同政治面貌（I1_6）的劳动者的平均收入是否相等
* 提示：政治面貌分为中共党员和非中共党员


#### 3.检验多个独立样本总体均值是否相等（样本组别 >= 3）
#### 案例（3-1）：以2015年总收入为例，试检验不同季节出生的劳动者的平均收入是否相等
* 虚无假设H0:不同季节出生的劳动者在2015年拥有相同的总收入
* 研究假设H1:不同季节出生的劳动者在2015年拥有不同的总收入
```
get file "clds2016_i.sav".
dataset name clds.
dataset activate clds.
select if (I3a_6  > 0).
select if (birthmonth > 0).
select if (gender > 0).
compute income = I3a_6.
recode birthmonth (3 thru 5 = 1)(6 thru 8 = 2)(9 thru 11 = 3)(12,1,2 = 4).
value labels birthmonth 1 "春季出生" 2 "夏季出生" 3 "秋季出生" 4 "冬季出生". 
variable level birthmonth (nominal).
means income by birthmonth
	/cells = mean count var stddev.
```

![方差分析](https://stat4soc.netlify.app/images/10.27.png)

#### 随堂练习：以2015年总收入为例，试检验不同行业的劳动者的平均收入是否相等


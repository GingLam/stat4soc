---
date: "2021-08-01T20:05:05-05:00"
tags:
- 数据清理
title: 第四节 数据整理与变量操作
---

#### 0.预处理
* 设置工作目录
```
cd "/Users/ginglam/Public/data".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```

![CLDS](https://stat4soc.netlify.app/images/4.1.png)

#### 1.通过转换既有变量类型，创建新变量
* 点选操作为：转换 -> 重新编码为不同变量 -> 左侧列表选择旧变量 -> 转移至变量框 -> 填写新变量名和标签 -> 点击变化量 -> 点击旧值和新值 -> 左侧填写旧值（或范围）和右侧填写新值 -> 添加新值 -> 重复所有取值 -> 填写缺失值 -> 继续 -> 确定（如设置错误，可点击“重置”再来一遍）
* 语法操作为：recode + 变量名 + (旧值 = 新值)（定类/定序变量取值加""）+ …… + into + 新变量名
* 定类/定序变量通过特定规则，可以转为定距/定比变量

#### 案例（1-1）：将最高教育程度变量"I2_1"转换为最高教育年限
* 转换规则
	* 未上过学 -> 0
	* 小学/私塾 -> 6
	* 初中 -> 9
	* 普通高中、职业高中、技校、中专 -> 12
	* 大专 -> 15
	* 本科 -> 16
	* 硕士 -> 19
	* 博士 -> 22
	* 其他、拒绝回答、不适用、不清楚 -> 系统缺失值
* 第一种方法
```
codebook I2_1.
recode I2_1 (1 = 0)
            (2 = 6)
	    (3 = 9)
	    (4 thru 7 = 12)
	    (8 = 15)
	    (9 = 16)
	    (10 = 19)
	    (11 = 22)
	    (else = sysmis) into edu_year.
```
* 等价于
```
recode I2_1 (1 = 0)
            (2 = 6)
	    (3 = 9)
	    (4,5,6,7 = 12)
	    (8 = 15)
	    (9 = 16)
	    (10 = 19)
	    (11 = 21)
	    (else = sysmis) into edu_year.
variable labels edu_year "教育年限".
variable level edu_year (scale).
codebook edu_year.
```

![edu_year](https://stat4soc.netlify.app/images/4.2.png)

* 定距/定比变量通过特定规则，可以转为定类/定序变量

#### 案例（1-2）：将2015年总收入变量"I3a_6"转换为收入等级变量
* 分类规则
	* 假设以2015年总收入是否超过6000元为中低门槛
	* 以2015年总收入是否超过2万元为中高门槛
	* 以2015年总收入是否超过4万元为高门槛
* 注意：相邻区间的边界取值，归入前一区间
```
codebook I3a_6.
descriptives I3a_6
	/statistics mean stddev min max.
recode I3a_6 (0 thru 6000 = 1)
             (6000 thru 20000 = 2)
	     (20000 thru 40000 = 3)
	     (40000 thru hi = 4) 
	     (else = sysmis) into income_rank.
variable labels income_rank "收入等级".
value labels income_rank 1 "低收入" 2 "中低收入" 3 "中高收入" 4 "高收入".
variable level income_rank (ordinal).
codebook income_rank.
```

![income_rank](https://stat4soc.netlify.app/images/4.3.png)

#### 2.通过外部公式计算新变量
* 点选操作：转换 -> 计算变量 -> 目标变量（输入新变量名） -> 数字表达式（输入公式） -> 确定
* 语法操作：compute + 新变量名 + "=" + 计算公式

#### 案例（2-1）：将2015年总收入变量"I3a_6"从人民币转换为美元（人民币兑换美元的汇率为6.2284：1）
```
compute income_dollars = I3a_6/6.2284.
compute income_dollars_m = income_dollars/12.
```

#### 随堂练习：计算2015年个人的劳动收入总额，包括主职工资收入（I3a_6_1）和兼职收入（I3b_5）


#### 3.通过变量间关系创建新变量
* 点选操作：转换 -> 计算变量 -> 目标变量（输入新变量名） -> 数字表达式（输入公式）-> 如果（填入运算条件）-> 继续 -> 确定
* 语法操作：
	* if + (变量须满足的条件) + 新变量名 + 赋值
	* 条件包括：&（并）、|（或）

#### 案例（3-1）：创建新变量“性别收入组”，包括男性高收入、男性低收入、女性高收入、女性低收入
* 分类规则
	* 假设以2015年总收入是否超过2万元为高收入门槛
	* 高于2万元的群体设定为高收入群体
	* 不高于2万元的群体设定为低收入群体
* 第一种方法
```
if (gender = 1 & income_rank >= 3) income_sex = 1.
if (gender = 1 & income_rank <= 2) income_sex = 2.
if (gender = 2 & income_rank >= 3) income_sex = 3.
if (gender = 2 & income_rank <= 2) income_sex = 4.
```
* 第二种方法
```
if(gender = 1 & range(I3a_6,20000,max(I3a_6))) income_sex = 1.
if(gender = 1 & range(I3a_6,min(I3a_6),20000)) income_sex = 2.
if(gender = 2 & range(I3a_6,20000,max(I3a_6))) income_sex = 3.
if(gender = 2 & range(I3a_6,min(I3a_6),20000)) income_sex = 4.
variable labels income_sex "性别收入组".
value labels income_sex 1 "男性高收入" 2 "男性低收入" 3 "女性高收入" 4 "女性低收入".
variable level income_sex (nominal).
codebook income_sex.
```

![income_rank](https://stat4soc.netlify.app/images/4.4.png)

#### 随堂练习：利用大学（I2_1）和收入（I3a_6）变量，创建新变量“学历收入组”，包括大学高收入、大学低收入、非大学高收入、非大学低收入.


#### 4.通过组间关系创建新变量
* 点选操作为：数据 -> 汇总 -> 分界变量（选择分组变量） -> 汇总变量（选择用以计算的变量） -> 函数（选择摘要统计）
-> 继续 -> 变量与标签 -> 确定
* 语法操作为：aggregate + /break（分组变量） + /新变量名 + "新变量标签" + /函数

#### 案例（4-1）：计算2015年家庭的总收入
* 注意：
	* 问卷编号"IID2016"为受访者的个人唯一识别码，即任意两人不相等
	* 问卷编号"FID2016"为受访者的家庭唯一识别码，即同一家庭内的受访者相等，但不同家庭的受访者不相等
	* 将同一家庭内所有成员在2015年总收入"I3a_6"进行加总，即可得到该家庭在2015年的总收入
```
aggregate 
	/break=FID2016 
	/household_income "家庭总收入" = sum(I3a_6).
variable level household_income (scale).
codebook household_income.
```

![income_rank](https://stat4soc.netlify.app/images/4.5.png)

#### 案例（4-2）：如何计算2015年家庭人口规模
* 第一种方法：设每一个家庭成员标记数为1，将同一个家庭成员的标记数加总
```
compute mark = 1.
aggregate 
	/break=FID2016 
	/household_num1 "家庭总人口" = sum(mark).
variable level household_num1 (scale).
descriptives household_num1
	/statistics mean stddev min max.
```

![income_rank](https://stat4soc.netlify.app/images/4.6.png)

* 第二种方法：计算相同家庭ID的数量，将结果赋予每一个体
```
aggregate 
	/break=FID2016 
	/household_num2 "家庭总人口" = n.
variable level household_num2 (scale).
descriptives household_num2
	/statistics mean stddev min max.
```

![income_rank](https://stat4soc.netlify.app/images/4.7.png)

#### 随堂练习：计算2015年家庭总劳动投入，用工作时间（I3a_1）来衡量


#### 5.变量的筛选
* 点选操作为：数据 -> 选择个案 -> 条件栏（选"如果条件满足"，点击"如果"）-> 选择变量 -> 在空白框输入条件（"|"指"或"，"&"指"并"）-> 继续 -> 输出框（选"删除未选定的个案"） -> 确定
* 语法操作为：select if + 条件或函数
* 注意：变量筛选前必须先检查异常值分布

#### 案例（5-1）：查看中国女性劳动者的2015年的收入状况
* 分析背景：
	* 根据OECD统计口径，筛选年龄为18~65岁的劳动人口（具备潜在劳动能力）
	* 根据性别状况，筛选女性人口
	* 根据收入状况，筛选有收入的人口
* 第一步：查看性别与出生年份变量的异常值分布
```
codebook gender birthyear.
descriptives gender birthyear
	/statistics mean stddev min max.
```

![income_rank](https://stat4soc.netlify.app/images/4.8.png)

* 第二步：对出生年份异常值进行处理
```
recode birthyear (2016 thru hi = sysmis)
                 (else = copy).
```
* 第三步：计算人口年龄
```
compute age = 2015 - birthyear.
variable labels age "年龄".
variable level age (scale).
descriptives age
	/statistics mean stddev min max.
```

![income_rank](https://stat4soc.netlify.app/images/4.9.png)

* 第四步：筛选劳动年龄、女性和有收入的人口
```
select if ((age  >= 18 & age <= 65) & gender = 2 & I3a_6 > 0).
descriptives I3a_6
	/statistics mean stddev min max.
```

![income_rank](https://stat4soc.netlify.app/images/4.10.png)

#### 随堂练习：查看中国老年劳动者的2015年收入状况
* 注意：
	* 筛选65岁以上人口
	* 筛选有工作的人口（work2015）


#### 6.个案（case）的排序（order）与排秩（rank）
* 点选操作为：
	* 数据 -> 个案排序 -> 选择变量（选至排序依据） -> 排序依据 -> 确定
	* 转换 -> 个案排秩 -> 选择变量 -> 类型排秩（选"秩"或"百分比秩"）-> 继续
* 语法操作为：
	* sort cases by + 变量 + (a)/(d)（升序或降序）
	* rank variable = + 变量 + (a)/(d)（升序或降序） + /rank（秩）或 /percent（百分比秩）

#### 案例（6-1）：查看女性受访者收入在全体女性劳动者中的排序与排秩
* 第一种方法：直接查看女性劳动收入排序
```
recode I3a_6 (sysmis = 0)
             (else = copy) into income.
variable labels income "收入".
variable level income (scale).
sort cases by income (a).
```

![income_rank](https://stat4soc.netlify.app/images/4.11.png)

* 第二种方法：创建一个新变量，显示女性劳动收入排序
```
compute sort_no = $income.
```

![income_rank](https://stat4soc.netlify.app/images/4.12.png)

#### 案例（6-2）：查看女性受访者收入在全体女性劳动者中的排秩
* 创建一个新变量，显示女性劳动收入排秩编号
```
rank variables =income (a)
	/rank.
```
* 注意比较排序与排秩的区别：排秩编号为同一排序编号的加权平均

![income_rank](https://stat4soc.netlify.app/images/4.13.png)


#### 随堂练习：计算收入前10%群体的收入总和占总体收入的比例，以此作为衡量社会不平等的指标




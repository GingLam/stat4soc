---
date: "2021-08-01T11:05:05-05:00"
tags:
- 推断统计
title: 第十一节 相关分析与简单回归分析
---

#### 0.预处理
* 设置工作目录
```
cd "F:/SPSS".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```

#### 1.两变量关系散点图
#### 案例（1-1）：描绘2015年主职总收入与最高教育年限的散点关系图
* 对2015年主职总收入与最高学历变量进行处理
```
select if (I3a_6  > 0).
select if (I2_1  > 0).
compute log_income = ln(I3a_6).
recode I2_1 (1 = 0)(2 = 6)(3 = 9)(4 thru 7 = 12)(8 = 15)(9 = 16)(10 = 19)(11 = 21)(else = sysmis) into edu_year.
```
* 利用图表构建器绘制散点图.
```
ggraph 
	/graphdataset name = "clds" variables = edu_year log_income
	/graphspec source = inline 
	/fitline total = yes. 
begin gpl
	SOURCE: clds = userSource(id("clds")) 
	DATA: edu_year=col(source(clds), name("edu_year")) 
	DATA: log_income=col(source(clds), name("log_income")) 
	GUIDE: axis(dim(1), label("教育年限")) 
	GUIDE: axis(dim(2), label("主职总收入（对数）")) 
	GUIDE: text.title(label("2015年最高教育年限与主职总收入关系")) 
	ELEMENT: point(position(edu_year*log_income)) 
end gpl.
```                                                                                                                                

#### 2.求两变量的协方差.
* **点选操作：**
	* 第一步：采用“可靠性分析”功能：分析 -> 标度 -> 可靠性分析 -> 从变量列表选中“因变量”和“自变量”并移入“项” -> 点击“统计” -> 勾选“项之间”的“协方差” -> 继续 -> 确定
	* 第二步：采用“可靠性分析”功能：分析 -> 标度 -> 可靠性分析 -> 从变量列表选中“因变量”和“自变量”并移入“项” -> 点击“统计” -> 勾选“摘要”的“协方差” -> 继续 -> 确定
	* 第三步：采用“双变量相关”功能：分析 -> 相关 -> 双变量 -> 从变量列表选中“因变量”和“自变量”并移入“变量” -> 点击“选项” -> 勾选“统计”的“叉积偏差和协方差” -> 继续 -> 确定
	* 第四步：采用“线性回归”功能：只能通过语法调用
* **语法操作：**
	* 采用“可靠性分析”功能：reliability + /variables + “因变量” “自变量” + /statistics（统计量） + “descriptives（呈现描述统计量，包括均值、标准差和个案数）”、“cov（协方差）”、“corr（相关系数）”、“anova（方差分析）”
	* 采用“可靠性分析”功能：reliability + /variables + “因变量” “自变量” + /summary（汇总结果） + “means（均值）”、“corr（相关系数）”、“anova（方差分析）”
	* 采用“双变量相关”功能：correlations + /variables + “因变量” “自变量” + /statistics（统计量） + “descriptives（呈现描述统计量，包括均值、标准差和个案数）” 或 “xprod（呈现叉积偏差和协方差）” 或“all（呈现以上两种结果）”
	* 采用“线性回归”功能：regression + /descriptives（描述表） + “means（均值）”、“variance（方差）”、“stddev（标准差）”、“cov（协方差）”、“corr（相关系数）”、“xprod（叉积偏差）”、“sig（显著度）”、“N（个案数）”、“all（全部）” + /statistics（统计量） + “R（相关系数和判定系数）”+ “coeff（回归系数）” + “anova（方差分析）” + “ci()（置信区间，如90、95、99）” + /dependent + “自变量” + /method = “enter（输入）”、“stepwise（步进）”、“remove（除去）”、“backward（后退）”、“forward（前进）”  + “因变量”.

#### 案例（2-1）：求2015年主职总收入与最高教育年限的协方差
* **第一种方法：根据统计公式计算**
* 第一步：计算2015年主职总收入（对数）的均值
```
aggregate 
	/log_income_mean = mean(log_income).
```
* 第二步：计算2015年主职总收入（对数）实际值与均值离差
```
compute log_income_diff = log_income-log_income_mean.
```
* 第三步：计算2015年最高教育年限的均值
```
aggregate 
	/edu_year_mean = mean(edu_year).
```
* 第四步：计算2015年最高教育年限实际值与均值的离差
```
compute edu_year_diff = edu_year-edu_year_mean.
```
* 第五步：创建新变量，使之等于主职总收入（对数）与最高教育年限的离差乘积
```
compute income_edu = log_income_diff*edu_year_diff.
```
* 第六步：对主职总收入（对数）与最高教育年限的离差乘积求总和
```
aggregate 
	/income_edu_sum = sum(income_edu).
```
* 第七步：用离差乘积之和除以自由度，得到两者的协方差
```
compute income_edu_cov = income_edu_sum/(1369-1).
formats income_edu_cov (f20.16).
descriptives income_edu_cov
	/statistics mean.
```
* **第二种方法：采用“可靠性分析”功能**
```
reliability 
	/variables log_income edu_year
	/statistics cov.
```
* **第三种方法：采用“可靠性分析”功能**
```
reliability 
	/variables log_income edu_year
	/summary cov.
```
* **第四种方法：采用“双变量相关”功能**
```
correlations 
	/variables log_income edu_year 
	/statistics xprod.
```
* **第五种方法：采用“线性回归”功能**
```
regression 
	/descriptives cov
	/statistics R 
	/dependent log_income 
	/method = enter edu_year.
```
* 由以上方法可知，2015年主职总收入与最高教育年限的协方差为2.657688


#### 3.求两变量的相关系数
* **点选操作：**
	* 第一步：采用“可靠性分析”功能：分析 -> 标度 -> 可靠性分析 -> 从变量列表选中“因变量”和“自变量”并移入“项” -> 点击“统计” -> 勾选“项之间”的“相关性” -> 继续 -> 确定
	* 第二步：采用“可靠性分析”功能：分析 -> 标度 -> 可靠性分析 -> 从变量列表选中“因变量”和“自变量”并移入“项” -> 点击“统计” -> 勾选“摘要”的“相关性” -> 继续 -> 确定
	* 第三步：采用“双变量相关”功能：分析 -> 相关 -> 双变量 -> 从变量列表选中“因变量”和“自变量”并移入“变量” -> 勾选“相关系数”的“皮尔逊” -> 确定
	* 第四步：采用“线性回归”功能：分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”和“自变量”并移入对应框 -> 点击“统计” -> 勾选“模型拟合” -> 继续 -> 确定
* **语法操作：** 参考第2节

#### 案例（3-1）：计算2015年主职总收入与最高教育年限的相关系数
* **第一种方法：根据统计公式计算**
* 第一步：计算2015年主职总收入（对数）的离差平方
```
compute log_income_diff_sq = log_income_diff**2.
```
* 第二步：计算2015年主职总收入（对数）的离差平方和
```
aggregate 
	/log_income_diff_sum = sum(log_income_diff_sq).
```
* 第三步：计算2015年主职总收入（对数）的总体方差估计值
```
compute log_income_var = log_income_diff_sum/(1369-1).
```
* 第四步：计算2015年主职总收入（对数）的总体标准差估计值
```
compute log_income_sd = sqrt(log_income_var).
```
* 第五步：计算2015年最高教育年限的离差平方
```
compute edu_year_diff_sq = edu_year_diff**2.
```
* 第六步：计算2015年最高教育年限的离差平方和
```
aggregate 
	/edu_year_diff_sum = sum(edu_year_diff_sq).
```
* 第七步：计算2015年最高教育年限的总体方差估计值
```
compute edu_year_var = edu_year_diff_sum/(1369-1).
```
* 第八步：计算2015年最高教育年限的总体标准差估计值
```
compute edu_year_sd = sqrt(edu_year_var).
```
* 第九步：计算2015年主职总收入（对数）与最高教育年限的标准差乘积，代表两者最大变异关系
```
compute income_edu_sd = log_income_sd*edu_year_sd.
```
* 第十步：用两者协方差占最大变异关系的比例，计算皮尔逊积矩相关系数
```
compute income_edu_corr = income_edu_cov/income_edu_sd.
formats income_edu_corr (f20.16).
descriptives income_edu_corr
	/statistics mean.
```
* **第二种方法：采用“可靠性分析”功能**
```
reliability 
	/variables log_income edu_year 
	/statistics corr.
```
* **第三种方法：采用“可靠性分析”功能**
```
reliability 
	/variables log_income edu_year 
	/summary corr.
```
* **第四种方法：采用“双变量相关”功能**
```
correlations 
	/variables log_income edu_year.
```
* **第五种方法：采用“线性回归”功能**
```
regression 
	/descriptives corr
	/statistics R 
	/dependent log_income 
	/method = enter edu_year.
```
* 由以上方法可知，2015年主职总收入与最高教育年限的相关系数为0.261340
* 由于相关系数具有对称性，可认为2015年最高教育年限可以解释26.134%的主职总收入，反之亦然


#### 4.回归分析
#### 4.1 回归系数的点估计
* **点选操作：** 分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”和“自变量”并移入对应框 -> 点击“统计” -> 勾选“回归系数”的“估算值” -> 继续 -> 确定
* **语法操作：** 参考第2节

#### 案例（4-1-1）：基于回归分析，用教育年限解释主要职业总收入
* **第一种方法：根据最小二乘法的统计公式计算**
* 第一步：用主职总收入（对数）与最高教育年限的离差乘积之和，除最高教育年限的离差平方和，得到最高教育年限的回归系数点估计值
```
compute beta_1 = income_edu_sum/edu_year_diff_sum.
```
* 第二步：根据主职总收入（对数）与最高教育年限的平均值，用拟合回归线方程，求出截距项
```
compute beta_0 = log_income_mean - beta_1*edu_year_mean.
formats beta_1 beta_0(f20.16).
descriptives beta_1 beta_0
	/statistics mean.
```
* **第二种方法：采用“线性回归”功能**
```
regression 
	/statistics coeff
	/dependent log_income 
	/method = enter edu_year.
```

#### 4.2 回归系数的区间估计
* **点选操作：** 分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”和“自变量”并移入对应框 -> 点击“统计” -> 勾选“回归系数”的“置信区间” -> 填入“置信区间” -> 继续 -> 确定
* **语法操作：** 参考第2节

#### 案例（4-2-1）：基于案例（4-1-1），计算回归系数的区间估计值
* 第一种方法：根据统计公式计算

#### 首先计算β_1的标准误差
* 根据拟合回归线方程，求出所有最高教育年限的预测主职总收入（对数）
```
compute log_income_hat = beta_0 + beta_1*edu_year.
```
* 用主职总收入实际值减去预测值，计算未得到解释的主职总收入量，即残差
```
compute log_income_dev = log_income - log_income_hat.
```
* 计算未解释的主职总收入量的平方和，即残差平方和
```
compute log_income_dev_sq = log_income_dev**2.
aggregate 
	/SSE = sum(log_income_dev_sq).
```
* 用残差平方和除自由度，得到模型的方差
```
compute beta_1_var = SSE/(1369-2).
```
* 用模型的方差与最高教育年限离差平方和之比的平方根，即最高教育年限系数估计值的标准误差
* 注意：要准确理解回归系数的标准误差的本质
	* 回归系数之所以存在标准误差，是因为系数估计值来源于随机抽样得到的样本，因而多次随机抽样计算的多个系数估计值本身构成一个抽样分布
	* 尽管系数估计值可以通过如下公式计算得到，但其本质与此无关，仅是Var(/hat(beta_1))的展开式作简化后的结果，详细证明见PPT正文
```
compute beta_1_se = sqrt(beta_1_var/edu_year_diff_sum).
```
* 95%的置信区间下，β_1的区间估计值
```
compute beta_1_upper = beta_1 + 1.959964*beta_1_se.
compute beta_1_lower = beta_1 - 1.959964*beta_1_se.
```

#### β_0的标准误差
```
compute edu_year_sq =  edu_year**2.
aggregate 
	/edu_year_sq_sum = sum(edu_year_sq).
compute beta_0_se = sqrt((edu_year_sq_sum*beta_1_var)/(1369*edu_year_diff_sum)).
```
* 95%的置信区间下，β_0的区间估计值
```
compute beta_0_upper = beta_0 + 1.959964*beta_0_se.
compute beta_0_lower = beta_0 - 1.959964*beta_0_se.
formats beta_1 beta_1_se beta_1_lower beta_1_upper beta_0 beta_0_se beta_0_lower beta_0_upper (f20.16).
descriptives beta_1 beta_1_se beta_1_lower beta_1_upper beta_0 beta_0_se beta_0_lower beta_0_upper
	/statistics mean.
```
* 第二种方法：采用“线性回归”功能
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter edu_year.
```
  
#### 4.3 拟合优度
* **点选操作：** 参考第3节
* **语法操作：** 参考第2节

#### 案例（4-3-1）：基于案例（4-1-1），计算回归模型的拟合优度
* **第一种方法：根据统计公式计算**
* R_2判定系数
```
compute SST = log_income_diff_sum.
compute SSE = log_income_dev_sum.
compute log_income_res = log_income_hat - log_income_mean.
compute log_income_res_sq = log_income_res**2.
aggregate 
	/SSR = sum(log_income_res_sq).
compute R_2 = SSR/SST.
formats SST SSE SSR R_2 (f20.16).
descriptives SST SSE SSR R_2
	/statistics mean.
```
* **第二种方法：采用“线性回归”功能**
```
regression 
	/statistics R 
	/dependent log_income 
	/method = enter edu_year.
```

#### 4.4 回归系数的假设检验
* **点选操作：** 参考第4.1节
* **语法操作：** 参考第2节

#### 案例（4-4-1）：基于案例（4-1-1），对回归模型系数值进行假设检验
* **第一种方法：根据统计公式计算**
```
compute beta_1_t = beta_1/beta_1_se.
compute beta_0_t = beta_0/beta_0_se.
formats beta_1_t beta_0_t (f20.16).
descriptives beta_1_t beta_0_t 
	/statistics mean.
compute beta_1_p = (1 - cdf.t(21.992008728164755,1367))*2.
compute beta_0_p = (1 - cdf.t(131.389621241004900,1367))*2.
formats beta_1_p beta_0_p (f20.16).
descriptives beta_1_p beta_0_p
	/statistics mean.
```
* **第二种方法：采用“线性回归”功能**
```
regression 
	/statistics coeff 
	/dependent log_income 
	/method = enter edu_year.
```

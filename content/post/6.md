---
date: "2021-08-01T18:05:05-05:00"
tags:
- 专题研究
title: 第七节 专题研究：当代中国的社会不平等
---

<script type="text/javascript" src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=default"></script>


#### 0.预处理
* 设置工作目录
```
cd "F:/SPSS".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```


#### 1.计算收入基尼系数
* 基尼系数的计算要点：
	* 用以计算的变量必须是定距变量或定比变量
	* 用以计算的变量不能包含负数
	* 用以计算的变量不能包含缺失值
	* 在最后计算基尼系数时，可以设置更长列宽和小数位，以现实更高计算精度
* 基尼系数的第一种计算方法：
	* 选择某一变量(\\(t\\))，根据其取值情况，排除负数值或缺失值样本；
	* 对该变量取值进行升序排序
	* 计算实际样本总量(\\(N\\))
	* 计算每个样本占实际样本总量的比重(\\(p\\))
	* 计算所有样本在该变量取值的总和(\\(T\\))
	* 计算每个样本在该变量的取值(\\(t\\))占其取值总和(\\(T\\))的份额(\\(P\\))
	* 计算该变量的份额(\\(P\\))的累积份额(\\(CP\\))
	* 用积分法计算该变量的基尼系数 \\(gini=1-\sum(p \cdot (2 \cdot CP-P))\\)
* 基尼系数的第二种计算方法：
	* 选择某一变量(\\(t\\))，根据其取值情况，排除负数值或缺失值样本
	* 对变量进行升序排序，并生成个案排序编号(\\(i\\))（注意，并非变量的“秩”）
	* 计算实际样本总量(\\(N\\))
	* 计算所有样本在该变量取值(\\(t\\))的总和(\\(T\\))
	* 以个案编号给所有样本在该变量上的取值加权 \\(w = t \cdot i\\)
	* 计算所有样本中该变量加权值的总和\\(tw\\)
	* 计算基尼系数 \\(gini=\frac{2\cdot tw}{(N \cdot T)}-1-\frac{1}{N}\\)

#### 第一种方法
* 第一步：排除主职总收入的负数值和缺失值
```
select if (I3a_6  >= 0).
```
* 第二步：对主职总收入进行升序排序
```
sort cases by I3a_6 (a).
```
* 第三步：计算实际分析的样本量
```
aggregate 
	/total_num = n.
```
* 第四步：计算每个人占实际样本总量的比重
```
compute id_percent = 1/total_num.
```
* 第五步：将所有人的主职收入加总
```
aggregate 
	/total_income = sum(I3a_6).
```
* 第六步：计算每个人的主职收入占样本主职收入总和的份额
```
compute income_percent = I3a_6/total_income.
```
* 第七步：计算每个人主职收入份额的累积份额
```
create cum_income=csum(income_percent).
```
* 第八步：使用基尼系数的计算公式
```
compute id_invert_gini = id_percent*(2*cum_income-income_percent).
aggregate 
	/invert_gini = sum(id_invert_gini).
compute gini = 1 - invert_gini.
formats gini (f20.16).
```
* 第九步：呈现基尼系数
```
descriptives gini
	/statistics mean.
```


#### 第二种方法
* 第一步：排除主职总收入的负数值和缺失值
```
select if (I3a_6  >= 0).
```
* 第二步：对主职总收入进行升序排序
```
sort cases by I3a_6 (a).
```
* 第三步：生成主职总收入的个案编号
```
compute case_num = $casenum.
```
* 第四步：计算实际分析的样本量
```
aggregate 
	/total_num = n.
```
* 第五步：将所有样本的主职收入加总
```
aggregate 
	/total_income = sum(I3a_6).
```
* 第六步：用个案编号计算主职收入的加权值
```
compute weight_income = case_num * I3a_6.
```
* 第七步：将所有样本的主职收入加权值进行加总
```
aggregate 
	/total_weight_income = sum(weight_income).
```
* 第八步：使用基尼系数的计算公式
```
compute gini = 2*total_weight_income/(total_num*total_income)-1-1/total_num.
formats gini (f20.16).
```
* 第九步：呈现基尼系数
```
descriptives gini
	/statistics mean.
```


#### 随堂练习：计算教育年限基尼系数（I2_1）
* 编码规则如下：
	* 未上过编码为0年
	* 上过小学/私塾编码为6年
	* 上过初中编码为9年
	* 上过普通高中/职业高中/技校/中专编码为12年
	* 上过大专编码为15年
	* 上过大学本科编码为16年
	* 上过硕士编码为19年
	* 上过博士编码为22年


#### 用Stata软件画劳伦兹曲线和计算基尼系数
* 以下为Stata命令(command)，请勿在SPSS操作
* 读取数据
```
cd "F:/SPSS"
use clds2016_i.dta,clear
```
* 安装Stata Module
```
ssc inst inequal7,all replace
ssc inst lorenz,all replace
```
* 画劳伦兹曲线
```
lorenz estimate I3a_6
lorenz graph, aspectratio(1)
```
* 计算基尼系数
```
inequal7 I3a_6
```

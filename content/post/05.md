---
date: "2021-08-01T19:05:05-05:00"
tags:
- 描述统计
title: 第五节 变量的基本描述统计
---

#### 0.预处理
* 设置工作目录
```
cd "/Users/ginglam/Public/data".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```

#### 1.探索性分析
* 点选操作为：分析 -> 描述报告 -> 探索 -> 填入因变量列表 -> 子因变量列表（分组变量） -> 选择统计量和图 -> 确定
* 语法操作为：examine variables =  + 变量名 + by 分组变量 + /plot（图形类型） + /statistics（统计量）
	* /plot（图形类型）：histograms（直方图）、boxplot（箱型图）、stemleaf（茎叶图）、npplot（正态分布图）
	* /statistics（统计量）：descriptives（全样本描述统计量）、extreme（极端值描述统计量）、none（不输出描述统计量）

#### 案例（1-1）：探索性别收入不平等
* 第一步：查看性别
```
codebook gender.
descriptives gender
	/statistics mean stddev min max.
recode gender (1 = 1)
              (2 = 0) into male.
variable labels male "性别（男性）".
value labels male 1 "男性" 0 "女性".
variable level male (nominal).
codebook male.
```

![CLDS](https://stat4soc.netlify.app/images/5.1.png)

* 第二步：查看2015年总收入信息
```
compute total_income = I3a_6.
variable labels total_income "2015总收入".
variable level total_income (scale).
descriptives total_income
	/statistics mean stddev min max.
```

![CLDS](https://stat4soc.netlify.app/images/5.2.png)

* 第三步：描述性别和2015年总收入关系
* 分别查看性别变量和2015年总收入变量
```
examine variables = gender total_income.
```
* 按性别类型查看2015年总收入变量
```
examine variables = total_income by gender
	/plot histograms  
	/statistics descriptives.
```

![CLDS](https://stat4soc.netlify.app/images/5.3.png)

#### 随堂练习：比较不同劳动年龄段总收入情况（按18-35岁、36-50岁、51-65岁划分）


#### 2.描述变量频率
* 点选操作为：分析 -> 描述统计 -> 频率 -> 填入统计量或图表 -> 继续 -> 确定
* 语法操作为：frequencies variables = + 变量 + 如下可选项
	* /format（格式）可选内容：avalue（变量取值升序）、devalue（变量取值降序）、afreq（变量数目升序）、dfreq（变量数目降序）
	* /barchart（条形图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /piechart（饼状图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /historgam（直方图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /statistics（统计量）可选内容：mean（均值）、semean（标准误均值）、median （中位数）、mode（众数）、stddev（标准差）、variance（方差）、 skewness（峰度）、 kurtosis（峰态）、range（取值范围）、 minimum（最小值）、 maximum（最大值）、 sum（总和）
	* /percentiles（分位数）：可填入分位数，用","隔开
* 注意：/statistics还可选default（默认mean、stddev、minimum、maximum）、all（全部）或none（无）

#### 案例：将2015年总收入划分等级
* 第一步：将定距型收入变量重新编码为定序型收入变量
```
recode I3a_6 (0 thru 6000 = 1)
             (6000 thru 20000 = 2)
	     (20000 thru 40000 = 3)
	     (40000 thru hi = 4) 
	     (else = sysmis) into income_rank.
variable labels income_rank "收入等级".
value labels income_rank 1 "低收入" 2 "中低收入" 3 "中高收入" 4 "高收入".
variable level income_rank (ordinal).
codebook income_rank.
```

![CLDS](https://stat4soc.netlify.app/images/5.4.png)

* 第二步：用条形图和饼状图描述2015年总收入等级
```
frequencies variables = income_rank
	/barchart freq.
```

![CLDS](https://stat4soc.netlify.app/images/5.5.png)

```
frequencies variables = income_rank
	/piechart percent.
```

![CLDS](https://stat4soc.netlify.app/images/5.6.png)

* 第三步：描述2015年总收入统计量
```
frequencies variables = I3a_6
	/statistics mean semean median mode stddev variance skewness kurtosis range minimum maximum sum
	/percentiles = 5,25,50,75,95.
```

![CLDS](https://stat4soc.netlify.app/images/5.7.png)

* 随堂练习：描述2015年平均每周工作时间（I3a_1），并将其划分为不充分就业（小于35小时）、正常就业（35-45小时）、过劳就业（超过45小时）


#### 3.列联表分析
* 点选操作为：分析 -> 描述统计 -> 交叉表 -> 填入"行"或"列" -> 填入统计量等 -> 继续 -> 确定
* 语法操作为：crosstabs + /tables = 行变量 + by + 列变量 + by + 层内变量…… + 如下可选项 
	* /cell（单元格信息）：count（计数）、row（按行计算百分比）、column（按列计算百分比）、total（行列综合百分比）、expected（期望值）、none（不显示）
	* /statistics（统计量）：chisq（卡方值）、phi（卡莱默系数）、cc（相依系数）、lambda（lambda系数）、gamma（gamma系数）、corr（皮尔逊相关系数）
	* /missing（报告缺失值）：include（包含在列联表内）、report（直接汇报）

#### 案例（3-1）：分析党员身份与收入等级关系
* 第一步：对党员身份进行重新编码
```
codebook I1_6.
descriptives I1_6
	/statistics mean stddev min max.
recode I1_6 (1 = 1) (2 thru 3 = 0) (else = sysmis) into be_ccp.
variable labels be_ccp "政治面貌（党员）".
value labels be_ccp 1 "党员" 0 "非党员".
variable level be_ccp (nominal).
codebook be_ccp.
```

![CLDS](https://stat4soc.netlify.app/images/5.8.png)

* 第二步：描述党员身份与个人总收入等级的关系
* 按党员身份查看个人总收入等级
```
crosstabs
	/tables = income_rank by be_ccp.
```

![CLDS](https://stat4soc.netlify.app/images/5.9.png)

* 按照个人收入等级查看党员身份
```
crosstabs
	/tables =  be_ccp by income_rank.
```

![CLDS](https://stat4soc.netlify.app/images/5.10.png)

* 按性别、党员身份查看个人总收入等级
```
crosstabs
	/tables =  be_ccp by income_rank by gender.
```	

![CLDS](https://stat4soc.netlify.app/images/5.11.png)


* 按党员身份查看个人总收入等级人数占比
```
crosstabs 
	/tables=income_rank by be_ccp 
	/cells = column
	/statistics = chisq.
```

![CLDS](https://stat4soc.netlify.app/images/5.12.png)


#### 随堂练习：描述政治面貌、劳动年龄与总收入的关系
* 注意：
	* 政治面貌为：党员、非党员
	* 劳动年龄为：青壮年（18-35岁）、中年（36-50岁）、中老年（51-65岁）
	* 总收入包括：按照4个分位划分为4个等级


#### 13.处理多选题（multiple choice question）
* 题目(1)：您主要通过何种途径了解国际新闻____（可多选）: A 报纸 B 杂志 C 广播 D 电视 E 电脑 F 手机 G 其他
	* 应将多选题转换为多道单选题，如题目(1-1)到题目（1-7）：
	* （1-1）您是否通过报纸了解国际新闻___：A 是 B 否
	* （1-2）您是否还有其他途径了解国际新闻___：A 是 B 否
* 点选操作为：参见第6-7节
* 语法操作为：
```
string news1 news2 news3 news4 news5 news6 news7 (a2).
variable labels news1 "您是否通过报纸了解国际新闻？" / 
	news2 "您是否通过杂志了解国际新闻？"/
	news3 "您是否通过广播了解国际新闻？"/
	news4 "您是否通过电视了解国际新闻？"/
	news5 "您是否通过电脑了解国际新闻？"/
	news6 "您是否通过手机了解国际新闻？"/
	news7 "您是否通过其他了解国际新闻？".
value labels news1 to news7 0 "否" 1 "是".
variable level news1 to news7 (nominal).
```


#### 14.处理缺失值（missing value）
* 题目（2）：您如何看待电商采集用户信息进行精准广告投放___ A 支持 B 理解 C 反对 D 说不清楚
* 题目（3）：平均而言，您每个月在所有电商平台上花多少钱___
* 注意：缺失值有两种情况：
	* 属于无效选项，如题目2选项D或选项E（显然，E并非既有选项）
	* 没有填答任何内容，如题目3回避填答或填答负值
* 点选操作为：
	* 题目（1）的处理为：变量视图 -> "缺失"列（双击待修改变量）-> 离散缺失值(填99) -> 确定
	* 题目（2）的处理为：变量视图 -> "缺失"列（双击待修改变量）-> 范围下限(填lo) -> 范围上限(填0) -> 离散值填9999999999 -> 确定
* 语法操作为：
	* 题目（1）的处理为：missing values + 变量名 + ("缺失值符码")
	* 题目（2）的处理为：missing values + 变量名 + (lo（负值） 9999999999（无填答）)
```
string ad (a2).
numeric consume (f10). 
variable labels 
	ad "您如何看待电商采集用户信息进行精准广告投放" /
	consume "平均而言，您每个月在所有电商平台上花多少钱".
value labels ad 1 "支持" 2 "理解" 3 "反对" 4 "说不清楚".
variable level ad (nominal) / consume (scale).
missing values ad ("4") consume (lo thru 0,9999999999).
```
* 如果0是有意义，而非缺失值，则进行以下处理：
```
missing values consume (lo thru -0.1, 9999999999).
```


#### 15.处理转跳题（conditional question）
* 题目（3-1）：您平时是否会在电商平台上买东西___（如选B，跳到题目3-4开始作答） A 会 B 不会
* 题目（3-2）：您平时会选择在哪个电商平台上买东西___（可多选） A 淘宝/天猫  B 京东 C 拼多多 D 微店 E Amazon F 其他
* 题目（3-3）：平均而言，您每个月在所有电商平台上花多少钱___
* 当数据编辑后进行变量操作，输出窗口将提示“变换处于挂起状态”，可用语法execute强制执行
```
execute.
delete variables consume.
string ol_shoping e_commerce1 e_commerce2 e_commerce3 e_commerce4 e_commerce5 e_commerce6 (a2).
numeric consume (f10).
variable labels 
	ol_shoping "您平时是否会在电商平台上买东西" /
	e_commerce1 "您平时是否会选择在淘宝或天猫上买东西" /
	e_commerce2 "您平时是否会选择在京东上买东西" /
	e_commerce3 "您平时是否会选择在拼多多上买东西" /
	e_commerce4 "您平时是否会选择在微店上买东西" /
	e_commerce5 "您平时是否会选择在亚马逊上买东西" /
	e_commerce6 "您平时是否还会选择在其他电商平台上买东西" /
	consume "平均而言，您每个月在所有电商平台上花多少钱".
value labels ol_shoping to e_commerce6 1 "会" 0 "不会".
variable level ol_shoping to e_commerce6 (nominal) / consume (scale).
```
*对于在题目3-1选B的受访者而言，他们在题目3-2和题目3-3中不作填答.
```
missing values e_commerce1 to e_commerce6 ("99") consume (lo thru 0,9999999999).
```


#### 16.输入真实数据
* 点选操作为：数据视图 -> 点击空白单元格 -> 输入数值
* 建议使用数据视图输入数据，避免串行或串位


#### 17.查看所有变量之间的基本信息
```
display dictionary.
```


#### 18.查看某一变量的详细编码信息（codebook）
* 点选操作为：分析 -> 报告 -> 代码本 -> 左列选中变量 -> 点击转移至"代码本" -> 确定
* 语法结构为：codebook + 变量名（单个或多个）
```
codebook education.
codebook education income.
```


#### 19.查看某一变量的基本统计量（statistics）
* 点选操作为：分析 -> 描述 -> 频率 -> 左列选中变量 -> 点击转移至"变量"列 -> 统计（勾选统计量）-> 继续 -> 确定
* 语法结构为：descriptives + 变量名 + /statistics + "统计量"
```
descriptives income
	/statistics mean stddev min max.
descriptives gender education income
	/statistics mean stddev min max.
```


#### 20.储存数据集.
* 储存所有变量，储存数据类型为SPSS
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 保存
* 语法结构为：save outfile = + "存储路径、变量名和后缀名.sav" + /compressed（替换已有同名数据）
```
save outfile = "data_0312.sav"
	/compressed.  
```

* 剔除变量gender和educetion，储存剩余变量，数据类型为SPSS
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：上述语法插入 /drop + 待剔除的变量名
```
save outfile = "data_0312.sav"
	/drop = gender education 
	/compressed.
```  

* 仅保留变量income，储存剩余变量，数据类型为SPSS
* 语法结构为：上述语法插入 /keep + 待保留的变量名
```
save outfile = "data_0312.sav"
	/keep = income 
	/compressed.
```

* 剔除变量gender和educetion，储存数据类型为Stata，版本号为14.0 ，版本类型为SE
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 选择保存类型为Stata -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：save translate outfile = + "存储路径、变量名和后缀名.dta" + Stata格式信息 + /replace（替换已有同名数据）+ /drop （或/keep） + 变量名
```
save translate outfile = "data_0312.dta"
	/type = Stata 
	/version = 14 
	/edition = se
	/replace 
	/drop = gender education.
```

* 剔除变量gender和educetion，储存数据类型为Excel 2013，以第一列作为变量名，以sample作为新工作表名
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 选择保存类型为Excel -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：save translate outfile = + "存储路径、变量名和后缀名.xlsx" + Excel格式信息 + /replace（替换已有同名数据）+ 变量名、储存数据范围和工作表 + /drop （或/keep） + 变量名
```
save translate outfile = "data_0312.xlsx"
	/type = xls
	/version = 12
	/replace 
	/fieldnames value = names 
	/cells = values 
	/exceloption sheet = "sample"
	/drop = gender education.
```
---
date: "2021-08-01T16:05:05-05:00"
tags:
- 推断统计
title: 第八节 总体分布、样本分布与抽样分布
---


#### 0.预处理
* 设置工作目录
```
cd "F:/SPSS".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```

#### 1.输出查看器
* 注意：SPSS输出查看器的显示结果无法直接清零，折衷的办法是重启输出查看器，实现清理效果
* 给输出查看器进行命名
```
output name clds_output.
```
* 激活输出查看器.
```
output activate clds_output.
```
* 关闭输出查看器.
```
output close clds_output.
```


#### 2.图表构建器
* 注意：图表构建器是SPSS的高级绘图功能，可以实现对图表全部特征的精准控制
* 点选操作为：图形 -> 图表构建器 -> 在图库中选择图形（如条形图、直线图、面积图、散点图和直方图等）并拖入空白处 -> 在左侧变量列表中选择变量并拖入空白处 -> 双击图表预览内容填入标题和坐标轴标签以及 -> 在右侧栏目设置元素属性、图表外观及其他选项 -> 确定
* 语法操作仅以条形图为例，分为两个部分，两部分必须同时执行缺一不可
	* ggraph 部分表示创建图形数据集和指定绘图变量
	* gpl……end gpl 部分表示图形生成语言（graphics production language），用以精确控制图表特征，并且gpl内部语法只能大写
* 基本语法结构为：
```
ggraph 
	/graphdataset name = "数据集" variables = 绘图变量名1（作为横轴） 绘图变量名2（作为纵轴，可以使用其统计量）
	/graphspec source = inline（即时绘图）或 /graphspec source = gplfile("XXX.gpl")（读取模板）.
begin gpl
	SOURCE: 绘图数据名 = userSource(id("数据集"))
	DATA: 绘图变量名1 = col(source(绘图数据名), name("绘图变量名1"), unit.category()（表示分类变量，括号内无需填入内容）)
	DATA: 绘图变量名2 = col(source(绘图数据名), name("绘图变量名2"))
	GUIDE: axis(dim(1), label("横轴名称"))
	GUIDE: axis(dim(2), label("纵轴名称"))
	GUIDE: text.title(label("标题名称")) 
	GUIDE: text.subtitle(label("副标题名称"))
	GUIDE: text.footnote(label("脚注名称"))
	SCALE: linear(dim(1), min(横轴下限), max(横轴上限)) 
	SCALE: linear(dim(2), min(横轴下限), max(横轴上限)) 
	ELEMENT: interval(position(绘图变量名1*绘图变量名2))
end gpl.
```

#### 案例（2-1）：绘制性别收入条形图
* 绘图要点：
	* 坐标轴横轴为性别，纵轴为不同性别的收入均值并生成变量“mean_income”
	* 坐标轴标签设定横轴为“性别”，纵轴为“收入均值”
	* 图表标题为“2015年不同性别的主职总收入图”，副标题为“基于CLDS2016数据”，脚注为“注：已排除缺失值”
	* 需事先排除主职总收入的负数值和缺失值
```
select if (I3a_6  >= 0).
compute income = I3a_6.
ggraph 
	/graphdataset name = "clds" variables= gender mean(income)[name = "mean_income"]
	/graphspec source = inline. 
begin gpl 
	SOURCE: clds=userSource(id("clds"))
	DATA: gender=col(source(clds), name("gender"), unit.category())
	DATA: mean_income=col(source(clds), name("mean_income"))
	GUIDE: axis(dim(1), label("性别"))
	GUIDE: axis(dim(2), label("平均收入"))
	GUIDE: text.title(label("2015年不同性别的主职总收入图")) 
	GUIDE: text.subtitle(label("基于CLDS2016数据"))
	ELEMENT: interval(position(gender*mean_income))
end gpl.
```


#### 3.绘制总体分布图(population distribution)
* 在总体分布图中同时呈现直方图和概率密度图需调用图表构建器

#### 案例（3-1）：绘制主职总收入的总体分布图
* 绘图要点：
	* 坐标轴标签设定横轴为“2015年主职总收入”，纵轴为“频率”
	* 坐标轴取值设定横轴范围为0到100万
	* 图表标题为“总体分布图(population distribution)”，副标题为“基于CLDS2016数据”，脚注为“注：已排除缺失值”
	* 直方图带宽为1万，图形的形状为方形，概率密度图选择用高斯方法拟合曲线
```
ggraph 
	/graphdataset name = "clds" variables=income
	/graphspec source = inline. 
begin gpl 
	SOURCE: income=userSource(id("clds")) 
	DATA: income=col(source(income), name("income")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("总体分布图(population distribution)")) 
	GUIDE: text.subtitle(label("基于CLDS2016数据"))
	GUIDE: text.footnote(label("注：已排除缺失值")) 
	SCALE: linear(dim(1), min(0), max(1000000)) 
	ELEMENT: interval(position(summary.count(bin.rect(income,binWidth(10000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(income))) 
end gpl.
```
* 计算主职总收入的总体均值
```
aggregate 
	/pop_income_mean = mean(income).
```
* 计算主职总收入的总体方差（方差的本质是“离差平方和的加权平均”）
* 计算主职总收入的总体离差平方（square of deviations）
```
compute diff_sq = (income - pop_income_mean)**2.
```
* 计算主职总收入的总体方差
```
aggregate 
	/pop_income_var = mean(diff_sq).
```
* 计算主职总收入的总体标准差
```
compute pop_income_sd = sqrt(pop_income_var).
```
* 呈现主职总收入的总体均值、总体方差和总体标准差
```
descriptives pop_income_mean pop_income_var pop_income_sd
	/statistics mean.
```


#### 4.绘制样本分布图(sample distribution).
* 设定随机种子：
	* 随机种子是计算机程序模拟随机化的方式之一
	* 随机种子可以设定任何取值，相同随机种子可以得到相同随机化结果

#### 案例（4-1）：绘制主职总收入的样本分布图
* 设定随机种子为20200423
```
set seed = 20200423.
```
* 从1462个总体中中抽取200个样本
* 注意：若抽取总体2%作为样本，语法为“sample 0.2.”
```
sample 200 from 1462.
```
* 绘制主职总收入的样本分布图
```
ggraph 
	/graphdataset name = "clds" variables=income
	/graphspec source = inline. 
begin gpl 
	SOURCE: income=userSource(id("clds")) 
	DATA: income=col(source(income), name("income")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("样本分布图(sample distribution)")) 
	GUIDE: text.subtitle(label("基于CLDS2016数据"))
	GUIDE: text.footnote(label("注：已排除缺失值")) 
	SCALE: linear(dim(1), min(0), max(1000000)) 
	ELEMENT: interval(position(summary.count(bin.rect(income,binWidth(10000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(income))) 
end gpl.
```
* 计算并呈现主职总收入的样本均值、样本方差和样本标准差
```
aggregate 
	/sam_income_mean = mean(income).
compute diff_sam_sq = (income - sam_income_mean)**2.
aggregate 
	/sam_income_var = mean(diff_sam_sq).
compute sam_income_sd = sqrt(sam_income_var).
descriptives sam_income_mean sam_income_var sam_income_sd
	/statistics mean.
```


#### 5.绘制抽样分布图(sampling distribution)
#### 案例（5-1）：绘制主职总收入抽样分布图
* 导入主职总收入数据
```
get stata file "simulation3/income_sample.dta".
dataset names sample window = front.
dataset activate sample.
```
* 变量sam_income_100表示样本数量为100、随机抽取500次的平均收入
```
ggraph 
	/graphdataset name = "sample" variables=sam_income_100
	/graphspec source = inline. 
begin gpl 
	SOURCE: sam_income_100=userSource(id("sample")) 
	DATA: sam_income_100=col(source(sam_income_100), name("sam_income_100")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("抽样分布图(sampling distribution)")) 
	GUIDE: text.subtitle(label("样本规模为100，随机抽取500次"))
	SCALE: linear(dim(1), min(10000), max(50000)) 
	ELEMENT: interval(position(summary.count(bin.rect(sam_income_100,binWidth(1000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(sam_income_100))) 
end gpl.
```
* 计算抽样分布的均值、方差和标准差
```
aggregate 
	/sam100_income_mean = mean(sam_income_100).
compute diff_sam100_sq = (sam_income_100 - sam100_income_mean)**2.
aggregate 
	/sam100_income_var = mean(diff_sam100_sq).
compute sam100_income_sd = sqrt(sam100_income_var).
```
* 变量sam_income_200表示样本数量为200、随机抽取500次的平均收入
```
ggraph 
	/graphdataset name = "sample" variables=sam_income_200
	/graphspec source = inline. 
begin gpl 
	SOURCE: sam_income_200=userSource(id("sample")) 
	DATA: sam_income_200=col(source(sam_income_200), name("sam_income_200")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("抽样分布图(sampling distribution)")) 
	GUIDE: text.subtitle(label("样本规模为200，随机抽取500次"))
	SCALE: linear(dim(1), min(10000), max(50000)) 
	ELEMENT: interval(position(summary.count(bin.rect(sam_income_200,binWidth(1000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(sam_income_200))) 
end gpl.
```
* 计算抽样分布的均值、方差和标准差
```
aggregate 
	/sam200_income_mean = mean(sam_income_200).
compute diff_sam200_sq = (sam_income_200 - sam200_income_mean)**2.
aggregate 
	/sam200_income_var = mean(diff_sam200_sq).
compute sam200_income_sd = sqrt(sam200_income_var).
```
* 变量sam_income_500表示样本数量为500、随机抽取500次的平均收入
```
ggraph 
	/graphdataset name = "sample" variables=sam_income_500
	/graphspec source = inline. 
begin gpl 
	SOURCE: sam_income_500=userSource(id("sample")) 
	DATA: sam_income_500=col(source(sam_income_500), name("sam_income_500")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("抽样分布图(sampling distribution)")) 
	GUIDE: text.subtitle(label("样本规模为500，随机抽取500次"))
	SCALE: linear(dim(1), min(10000), max(50000)) 
	ELEMENT: interval(position(summary.count(bin.rect(sam_income_500,binWidth(1000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(sam_income_500))) 
end gpl.
aggregate 
	/sam500_income_mean = mean(sam_income_500).
compute diff_sam500_sq = (sam_income_500 - sam500_income_mean)**2.
aggregate 
	/sam500_income_var = mean(diff_sam500_sq).
compute sam500_income_sd = sqrt(sam500_income_var).
```
* 变量sam_income_1000表示样本数量为1000、随机抽取500次的平均收入
```
ggraph 
	/graphdataset name = "clds" variables=sam_income_100
	/graphspec source = inline. 
begin gpl 
	SOURCE: sam_income_100=userSource(id("clds")) 
	DATA: sam_income_100=col(source(sam_income_100), name("sam_income_100")) 
	GUIDE: axis(dim(1), label("2015年主职总收入")) 
	GUIDE: axis(dim(2), label("频率")) 
	GUIDE: text.title(label("抽样分布图(sampling distribution)")) 
	GUIDE: text.subtitle(label("样本规模为1000，随机抽取500次"))
	SCALE: linear(dim(1), min(10000), max(50000)) 
	ELEMENT: interval(position(summary.count(bin.rect(sam_income_100,binWidth(1000)))),shape.interior(shape.square))
	ELEMENT: line(position(density.kernel.gaussian(sam_income_100))) 
end gpl.
```
* 计算抽样分布的均值、方差和标准差
```
aggregate 
	/sam1000_income_mean = mean(sam_income_1000).
compute diff_sam1000_sq = (sam_income_1000 - sam1000_income_mean)**2.
aggregate 
	/sam1000_income_var = mean(diff_sam1000_sq).
compute sam1000_income_sd = sqrt(sam1000_income_var).
```
* 呈现不同样本规模抽样分布的均值、方差和标准差.
```
descriptives sam100_income_mean sam100_income_var sam100_income_sd
             sam200_income_mean sam200_income_var sam200_income_sd
			 sam500_income_mean sam500_income_var sam500_income_sd
			 sam1000_income_mean sam1000_income_var sam1000_income_sd	    
	/statistics mean.
```


#### 6.根据总体情况计算抽样分布
#### 案例（6-1）：以2015年主职总收入为例，计算抽样分布
* 导入数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
select if (I3a_6  >= 0).
compute income = I3a_6.
```
* 总体已知，利用总体信息
```
aggregate 
	/pop_income_mean = mean(income).
compute diff_sq = (income - pop_income_mean)**2.
aggregate 
	/pop_income_var = mean(diff_sq).
compute sampling_pop_in_var = pop_income_var/200.
compute sampling_pop_in_sd = sqrt(sampling_pop_in_var).
descriptives sampling_pop_in_sd
	/statistics mean.
```
* 总体未知，利用样本信息
```
set seed = 20200423.
sample 200 from 1462.
aggregate 
	/sam_income_mean = mean(income).
compute diff_sam_sq = (income - sam_income_mean)**2.
aggregate 
	/diff_sam_sq_sum = sum(diff_sam_sq).
compute sam_income_var = diff_sam_sq_sum/199.
compute sampling200_in_var = sam_income_var/200.
compute sampling200_in_sd = sqrt(sampling200_in_var).
descriptives sampling200_in_sd
	/statistics mean.
```
* 可以使用descriptives语法直接报告结果
* 注意：descriptives语法报告的结果，均把当前数据集视为样本而非总体，其结果已进行贝塞尔校正
```
descriptives income 
	/statistics=mean stddev variance min max semean.
```
  
  
#### 7.根据样本信息估计总体的置信区间
* 点选操作为：分析 -> 描述统计 -> 探索 -> 在左侧变量列表中选择变量 -> 选择统计 -> 勾选描述并输入置信区间 -> 确定
* 语法操作参考第五周探索性分析
#### 案例（7-1）：以2015年主职总收入为例，估计总体的置信区间
* 导入数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
select if (I3a_6  >= 0).
compute income = I3a_6.
examine variables = income
	/cinterval 95.
```


#### 8.绘制教育年限的抽样分布.
* 预设知道总体信息
	* 教育年限总体均值=8.667468，总体标准差=4.223336，总体个数=2081
	* 若教育年限的样本个数为2005，其理想抽样分布为正态分布N(8.667468,0.00889604)
* 以下为Stata命令
```
#delimit;
graph twoway 
	(function y=normalden(x,8.667468,0.09431883), range(8.35 8.99) lw(medthick)),
	xline(8.667468, lpattern(dash))
	title("Normal Distribution")
	xtitle("education(sample mean)", size(medlarge)) ytitle("")
    ylabel("",nogrid)
	xlabel(8.35(0.1)8.99)
	xscale(lw(medthick)) yscale(lw(medthick))
	graphregion(fcolor(white));
#delimit cr
```


#### 9.将教育年限的抽样分布标准化，绘制标准正态分布
* 以下为Stata命令
```
#delimit;
graph twoway
	(function y=normalden(x),range(-4 4)),
	xline(0,lpattern(dash))
    title("Standard Normal Distribution")
	ylabel(0(0.05)0.45,nogrid)
	xtitle("standardized education(sample mean)", size(medlarge)) ytitle("density")
	graphregion(fcolor(white));
#delimit cr 
```

#### 随堂练习：根据2015年平均每周工作时间（I3a_1）计算95置信区间
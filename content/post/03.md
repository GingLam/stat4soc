---
date: "2021-08-01T21:05:05-05:00"
tags:
- 问卷录入
title: 第三节 问卷编码与数据录入（二）
---

#### 13.处理多选题（multiple choice question）
* 点选操作与语法操作参见第6-7节

#### 案例（13-1）：将以下多选题录入SPSS
* 您主要通过何种途径了解国际新闻____（可多选）: A 报纸 B 杂志 C 广播 D 电视 E 电脑 F 手机 G 其他
* 应将多选题转换为多道单选题：
	* 您是否通过报纸了解国际新闻___：A 是 B 否
	* 您是否还有其他途径了解国际新闻___：A 是 B 否
```
string news1 news2 news3 news4 news5 news6 news7 (a2).
variable labels news1 "您是否通过报纸了解国际新闻？" / 
	        news2 "您是否通过杂志了解国际新闻？"/
	        news3 "您是否通过广播了解国际新闻？"/
	        news4 "您是否通过电视了解国际新闻？"/
	        news5 "您是否通过电脑了解国际新闻？"/
	        news6 "您是否通过手机了解国际新闻？"/
	        news7 "您是否通过其他了解国际新闻？".
value labels news1 to news7 0 "否" 1 "是".
variable level news1 to news7 (nominal).
```

![处理多选题](https://stat4soc.netlify.app/images/3.1.png)

#### 14.处理缺失值（missing value）
* 点选操作为：
	* 题目（1）的处理为：变量视图 -> "缺失"列（双击待修改变量）-> 离散缺失值(填99) -> 确定
	* 题目（2）的处理为：变量视图 -> "缺失"列（双击待修改变量）-> 范围下限(填lo) -> 范围上限(填0) -> 离散值填9999999999 -> 确定
* 语法操作为：
	* 题目（1）的处理为：missing values + 变量名 + ("缺失值符码")
	* 题目（2）的处理为：missing values + 变量名 + (lo（负值） 9999999999（无填答）)

#### 案例（14-1）：将以下题目录入SPSS并处理缺失值
* 您如何看待电商采集用户信息进行精准广告投放___ A 支持 B 理解 C 反对 D 说不清楚
* 平均而言，您每个月在所有电商平台上花多少钱___
* 注意，缺失值的出现有两种情况：
	* 属于无效选项，如题目2选项D或选项E（显然，E并非既有选项）
	* 没有填答任何内容，如题目3回避填答或填答负值
```
string ad (a2).
numeric consume (f10). 
variable labels ad "您如何看待电商采集用户信息进行精准广告投放" /
	        consume "平均而言，您每个月在所有电商平台上花多少钱".
value labels ad 1 "支持" 2 "理解" 3 "反对" 4 "说不清楚".
variable level ad (nominal) / consume (scale).
missing values ad ("4") consume (lo thru 0,9999999999).
```

![将以下题目录入SPSS并处理缺失值](https://stat4soc.netlify.app/images/3.2.png)

* 如果0是有意义，而非缺失值，则进行以下处理：
```
missing values consume (lo thru -0.1, 9999999999).
```

![将以下题目录入SPSS并处理缺失值](https://stat4soc.netlify.app/images/3.3.png)

#### 15.处理转跳题（conditional question）
#### 案例（15-1）：将以下转调题目录入SPSS
* 您平时是否会在电商平台上买东西___（如选B，跳到题目3-4开始作答） A 会 B 不会
* 您平时会选择在哪个电商平台上买东西___（可多选） A 淘宝/天猫  B 京东 C 拼多多 D 微店 E Amazon F 其他
* 平均而言，您每个月在所有电商平台上花多少钱___
```
string ol_shoping e_commerce1 e_commerce2 e_commerce3 e_commerce4 e_commerce5 e_commerce6 (a2).
numeric consume (f10).
variable labels 
	ol_shoping "您平时是否会在电商平台上买东西" /
	e_commerce1 "您平时是否会选择在淘宝或天猫上买东西" /
	e_commerce2 "您平时是否会选择在京东上买东西" /
	e_commerce3 "您平时是否会选择在拼多多上买东西" /
	e_commerce4 "您平时是否会选择在微店上买东西" /
	e_commerce5 "您平时是否会选择在亚马逊上买东西" /
	e_commerce6 "您平时是否还会选择在其他电商平台上买东西" /
	consume "平均而言，您每个月在所有电商平台上花多少钱".
value labels ol_shoping to e_commerce6 1 "会" 0 "不会".
variable level ol_shoping to e_commerce6 (nominal).
variable level consume (scale).
```
* 注意：对于第1题选B的受访者而言，他们在第2-3题中不必作填答
```
missing values e_commerce1 to e_commerce6 ("99") consume (lo thru 0,9999999999).
```

![处理转跳题](https://stat4soc.netlify.app/images/3.4.png)

#### 16.输入真实数据
* 点选操作为：数据视图 -> 点击空白单元格 -> 输入数值
* 建议使用数据视图输入数据，避免串行或串位
#### 案例（16-1）：创建一个名为edu数据集，录入如下数据
| 受访者编号 	| 教育程度 	| 每月收入 	|
|---	|---	|---	|
| 1 	| 初中 	| 3600 	|
| 2 	| 大学 	| 7000 	|
| 3 	| 小学未毕业 	| 9000 	|
| 4 	| 高中 	| 6500 	|
| 5 	| 大学 	| 12000 	|
| 6 	| 硕士 	| 14000 	|
```
new file.
dataset names edu window = front.
dataset activate edu.
string education (a2).
numeric income (f10).
variable labels education "教育程度" /
                income "每月收入".
value labels education 1 "小学及以下" 2 "初中" 3 "高中" 4 "大学及以上".
variable level education (ordinal) / 
    	       income(scale).
```

![处理转跳题](https://stat4soc.netlify.app/images/3.5.png)
![处理转跳题](https://stat4soc.netlify.app/images/3.6.png)

#### 17.查看所有变量之间的基本信息
```
display dictionary.
```

![查看所有变量之间的基本信息](https://stat4soc.netlify.app/images/3.7.png)

#### 18.查看某一变量的详细编码信息（codebook）
* 点选操作为：分析 -> 报告 -> 代码本 -> 左列选中变量 -> 点击转移至"代码本" -> 确定
* 语法结构为：codebook + 变量名（单个或多个）
#### 案例（18-1）：查看变量education和income的详细编码信息
```
codebook education.
codebook education income.
```

![处理转跳题](https://stat4soc.netlify.app/images/3.8.png)

#### 19.查看某一变量的基本统计量（statistics）
* 点选操作为：分析 -> 描述 -> 频率 -> 左列选中变量 -> 点击转移至"变量"列 -> 统计（勾选统计量）-> 继续 -> 确定
* 语法结构为：descriptives + 变量名 + /statistics + "统计量"
#### 案例（19-1）：查看变量education和income的基本统计量
```
descriptives education income
	/statistics mean stddev min max.
```
* 注意：education为定类变量，无法计算均值和标准差

![处理转跳题](https://stat4soc.netlify.app/images/3.9.png)

#### 20.储存数据集
* 储存所有变量，储存数据类型为SPSS
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 保存
* 语法结构为：save outfile = + "存储路径、变量名和后缀名.sav" + /compressed（以压缩的方式存储数据）
#### 案例（20-1）：将数据存为demo，格式为SPSS专用格式
```
save outfile = "demo.sav"
	/compressed.  
```
#### 案例（20-2）：将数据存为demo，剔除变量education，储存剩余变量，格式为SPSS专用格式
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：上述语法插入 /drop + 待剔除的变量名
```
save outfile = "demo.sav"
	/drop = education 
	/compressed.
```  
#### 案例（20-3）：将数据存为demo，仅保留变量income，储存剩余变量，格式为SPSS专用格式
* 语法结构为：上述语法插入 /keep + 待保留的变量名
```
save outfile = "demo.sav"
	/keep = income 
	/compressed.
```
#### 案例（20-4）：将数据存为demo，剔除变量education，储存剩余变量，格式为Stata专用格式，版本号为14.0 ，版本类型为SE
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 选择保存类型为Stata -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：save translate outfile = + "存储路径、变量名和后缀名.dta" + Stata格式信息 + /replace（替换已有同名数据）+ /drop （或/keep） + 变量名
```
save translate outfile = "demo.dta"
	/type = Stata 
	/version = 14 
	/edition = se
	/replace 
	/drop = education.
```
#### 案例（20-4）：将数据存为demo，剔除变量education，储存剩余变量，储存数据类型为Excel2013专用格式，以第一列作为变量名，以sample作为新工作表名
* 点选操作为：文件 -> 另存为 -> 选择存储路径 -> 输入文件名 -> 选择保存类型为Excel -> 变量 -> 选择保留 -> 继续 -> 保存
* 语法结构为：save translate outfile = + "存储路径、变量名和后缀名.xlsx" + Excel格式信息 + /replace（替换已有同名数据）+ 变量名、储存数据范围和工作表 + /drop （或/keep） + 变量名
```
save translate outfile = "demo.xlsx"
	/type = xls
	/version = 12
	/replace 
	/fieldnames value = names 
	/cells = values 
	/exceloption sheet = "sample"
	/drop = gender education.
```

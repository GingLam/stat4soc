---
date: "2021-08-01T10:05:05-05:00"
tags:
- 推断统计
title: 第十二讲 多元回归分析
---


#### 0.预处理
* 设置工作目录
```
cd "/Users/ginglam/Public/data".
```
* 导入2016年中国劳动力动态调查数据.
```
get file "clds2016_i.sav".
dataset name clds.
dataset activate clds.
```

#### 1.多元回归分析
* 点选操作：分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”及两个或多个“自变量”并移入对应框 -> 点击“统计” -> 勾选“回归系数”的“估算值”和“置信区间” -> 继续 -> 确定
* 语法操作：参见前面讲义

#### 案例（1-1）：探索2015年总收入的影响因素
* 第一步：对2015年总收入、年龄与教育年限变量进行处理
```
select if (I3a_6  > 0).
compute log_income = ln(I3a_6).
select if (birthyear > 0).
compute age = 2015-birthyear.
select if (I2_1 > 0).
recode I2_1 (1 = 0)(2 = 6)(3 = 9)(4 thru 7 = 12)(8 = 15)(9 = 16)(10 = 19)(11 = 21)(else = sysmis) into edu_year.
```
* 第二步：采用多元回归模型探索2015年总收入的影响因素
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter age edu_year.
```

![大数定律](https://stat4soc.netlify.app/images/12.1.png)

#### 随堂练习：探索生活幸福感（I7_6_1）的影响因素
* 提示：可能影响因素包括年龄、教育年限和收入水平


#### 2.回归分析的前提假设检验.
* 回归模型的独立同分布假设在实际研究中最受关注
#### 2.1 用Durbin-Watson值检验残差项相互独立.
* **点选操作：** 分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”及两个或多个“自变量”并移入对应框 -> 点击“统计” -> 在“残差”项勾选“德宾-沃森” -> 继续 -> 确定
* **语法操作：** 参见前面讲义
```
regression 
	/statistics r
	/dependent log_income 
	/method = enter age edu_year
	/residuals durbin.
```

![大数定律](https://stat4soc.netlify.app/images/12.2.png)

* 一般而言，Durbin-Watson值的取值范围在0-4之间，取值越接近2，残差项相互独立的可能性越高
* 由结果可知，Durbin-Watson值为1.658，可近似认为条件2.1满足

#### 2.2 用残差图判断同方差性
* **点选操作：** 分析 -> 回归 -> 线性 -> 从变量列表选中“因变量”及两个或多个“自变量”并移入对应框 -> 点击“图” -> 选择“*zresid”移入“y”，“*zpred”移入“x” -> 继续 -> 确定
* **语法操作：** 参见前面讲义
```
regression 
	/statistics r
	/dependent log_income 
	/method = enter age edu_year
	/scatterplot=(*zresid,*zpred).
```

![大数定律](https://stat4soc.netlify.app/images/12.3.png)

![大数定律](https://stat4soc.netlify.app/images/12.4.png)

* 由结果可知，标准化残差值在每一个预测值处均匀分布，认为该回归模型满足同方差性
  
  
#### 3.理解控制变量的本质
#### 案例（3-1）：探索2015年总收入与教育年限的关系
* 第一步：构造关于在教育年限（核心自变量）与总收入的回归模型，即基础模型或基准模型
* 第二步：在基础模型中加入年龄项（控制变量），构造拓展模型。拓展模型的作用是剔除年龄的作用，以获得教育年限对总收入的净影响
* 注意：
	* 核心自变量和控制变量都是自变量
	* 基础模型与拓展模型互为嵌套模型，基础模型嵌套于拓展模型
* **基础模型**
```
regression
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter edu_year.
```

![大数定律](https://stat4soc.netlify.app/images/12.5.png)

* **拓展模型**
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter edu_year age.
```

![大数定律](https://stat4soc.netlify.app/images/12.6.png)

* **同时呈现基础模型和拓展模型**
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter edu_year
	/method = enter edu_year age.
```

![大数定律](https://stat4soc.netlify.app/images/12.7.png)

* 控制“年龄”这个变量，本质上就是控制年龄与教育年限的线性相依性，即年龄与教育的“内在共同因素”.可以通过两种方式进行验证

* **第一种方式：在模型增加年龄与教育年限的线性相依性作为控制变量**
* 用年龄预测教育年限，将预测结果作为包含两者“线性相依性”的变量
```
regression 
	/statistics coeff ci(95) 
	/dependent edu_year
	/method = enter age.
compute edu_year_pred =(-0.144291*age)+15.209316.
```
* 用教育年限预测收入，同时添加年龄与教育年限的“线性相依性”作为控制变量
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income
	/method = enter edu_year edu_year_pred.
```

![大数定律](https://stat4soc.netlify.app/images/12.8.png)

* **第二种方式：在模型中排除年龄与教育年限的线性相依性**
* 用年龄预测收入，并将实际收入与预测收入之差，作为排除年龄作用的结果
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter age.
compute log_income_pred =(-0.031963*age)+11.205566.
compute log_income_resid = log_income - log_income_pred.
compute edu_year_resid = edu_year - edu_year_pred.
regression 
	/statistics coeff ci(95) 
	/dependent log_income_resid 
	/method = enter edu_year_resid.
```

![大数定律](https://stat4soc.netlify.app/images/12.9.png)

#### 4.处理自变量与因变量的非线性关系
#### 案例（4-1）：探索2015年总收入与年龄的关系
* 查看不同年龄组的收入差异
```
descriptives age.
recode age (16 thru 30 = 1)(31 thru 40 = 2)(41 thru 50 = 3)(51 thru 60 = 4)(61 thru hi = 5)(else = sysmis) into age_group.
value labels age_group 1 "16-30岁" 2 "31-40岁" 3 "41-50岁" 4 "51-60岁" 5 "61岁以上". 
variable level age_group (ordinal).
means log_income by age_group
	/cells = mean count var stddev.
```

![大数定律](https://stat4soc.netlify.app/images/12.10.png)

#### 案例（4-2）：通过散点图探索2015年总收入与年龄的关系
* 利用图表构建器绘制散点图和拟合线
```
ggraph 
	/graphdataset name = "clds" variables = age log_income
	/graphspec source = inline 
	/fitline total = yes. 
begin gpl
	SOURCE: clds = userSource(id("clds")) 
	DATA: age=col(source(clds), name("age")) 
	DATA: log_income=col(source(clds), name("log_income")) 
	GUIDE: axis(dim(1), label("年龄平方")) 
	GUIDE: axis(dim(2), label("总收入（对数）")) 
	GUIDE: text.title(label("2015年劳动者年龄与总收入关系")) 
	ELEMENT: point(position(age*log_income)) 
end gpl.
```

![大数定律](https://stat4soc.netlify.app/images/12.11.png)

* 利用曲线估算功能绘制散点图和拟合线
* **点选操作：** 分析 -> 回归 -> 曲线估算 -> 从变量列表选中“因变量”并移入“因变量”框 -> 从变量列表选中“自变量”并移入“独立变量”框 -> 在模型中勾选“线性”或“二次”等 -> 确定
* **语法操作：** curvefit + /variables = + “因变量” + “自变量” + /constant（保留截距项）+ /model = quadratic（选择用平方项拟合模型） +  /plot fit（输出拟合图）
* 第一步：拟合年龄与收入直线模型（linear model）
```
curvefit 
	/variables = log_income with age 
	/constant 
	/plot fit.
```

![大数定律](https://stat4soc.netlify.app/images/12.12.png)

* 第二步：拟合年龄与收入曲线模型（curve model）或非线性模型（non-linear model）
```
curvefit 
	/variables = log_income with age 
	/constant 
	/model = quadratic 
	/plot fit.
```

![大数定律](https://stat4soc.netlify.app/images/12.13.png)

* 第三步：拟合年龄平方项与收入直线模型（linear model）
```
compute age_sq = age**2.
curvefit 
	/variables = log_income with age_sq 
	/constant 
	/plot fit.
```

![大数定律](https://stat4soc.netlify.app/images/12.14.png)
  
#### 案例（4-3）：通过回归分析探索2015年总收入与年龄的关系
* 第一步：构建年龄平方项与收入的线性模型
```
regression 
	/statistics coeff
	/dependent log_income 
	/method = enter age age_sq.
```

![大数定律](https://stat4soc.netlify.app/images/12.15.png)

* 第二步：根据线性模型，基于年龄平方项对收入进行预测
```
compute log_income_pred =0.066585*age+(-0.001099*age_sq)+9.180541.
curvefit 
	/variables = log_income_pred with age
	/model = quadratic  
	/constant 
	/plot fit.
```

![大数定律](https://stat4soc.netlify.app/images/12.16.png)

* 第三步：计算劳动者取得最高收入的年龄时点
	* 基于线性模型的一阶导数，求因变量拐点的自变量取值
```
compute turn_point = -(0.066585/(2*(-0.001099))).
formats turn_point (f20.16).
descriptives turn_point
  /statistics mean.
```

![大数定律](https://stat4soc.netlify.app/images/12.17.png)
 
#### 5.定类和定序自变量的处理
* **当学历变量是定类变量时**
#### 案例（5-1）：探索2015年总收入与大学学历的关系
* 第一步：构造大学学历变量
```
recode I2_1 (1 thru 7 = 0)(8 thru hi = 1)(else = sysmis) into be_college.
value labels be_college 0 "没上过大学" 1 "上过大学". 
variable level be_college (nominal).
```
* 第二步：以没上过大学为参照组，以上过大学作为核心自变量进行回归分析
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter be_college age.
```

![大数定律](https://stat4soc.netlify.app/images/12.18.png)

* **当学历变量是定序变量时**
#### 案例（5-2）探索2015年总收入与不同学历的关系
* 第一步：构造多阶段学历变量
```
recode I2_1 (1 thru 2 = 1)(3 = 2)(4 thru 7 = 3)(8 thru hi = 4)(else = sysmis) into qualification.
value labels qualification 1 "小学及以下" 2 "初中" 3 "高中" 4 "大学及以上". 
variable level qualification (ordinal).
```
* 第二步：将多阶段学历变量转为多个虚拟变量，分别代表小学及以下、初中、高中、大学及以上
```
spssinc create dummies variable = qualification
rootname1 = qual.
descriptives qual_1 qual_2 qual_3 qual_4.
```

![大数定律](https://stat4soc.netlify.app/images/12.19.png)

* 第三步：以小学及以下学历作为参照组，以初中、高中和大学及以上作为核心自变量进行回归分析
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter qual_2 qual_3 qual_4 age.
```

![大数定律](https://stat4soc.netlify.app/images/12.20.png)

#### 随堂练习：探索生活幸福感（I7_6_1）与教育的关系
* 提示：用多阶段学历衡量教育（以大学及以上学历作为参照组）并作为核心解释变量，以性别、年龄和收入水平作为控制变量


#### 6.回归模型的标准化
* **点选操作：** 参见第1节
* **语法操作：** 参见第1节

#### 案例（6-1）：利用多元回归模型探索2015年总收入的影响因素
```
regression 
	/statistics coeff ci(95) 
	/dependent log_income 
	/method = enter age edu_year.
```

![大数定律](https://stat4soc.netlify.app/images/12.21.png)


---
date: "2021-08-02T21:05:05-05:00"
tags:
- 问卷
title: 第四节 数据清理的基本操作 
---

#### 0.预处理
* 设置工作目录（working directory）
```
cd "F:/SPSS".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```


#### 1.通过转换既有变量类型，创建新变量
* 点选操作为：转换 -> 重新编码为不同变量 -> 左侧列表选择旧变量 -> 转移至变量框 -> 填写新变量名和标签 
-> 点击变化量 -> 点击旧值和新值 -> 左侧填写旧值（或范围）和右侧填写新值 -> 添加新值 -> 重复所有取值 
-> 填写缺失值 -> 继续 -> 确定（如设置错误，可点击“重置”再来一遍）
* 语法操作为：recode + 变量名 + (旧值 = 新值)（定类/定序变量取值加""）+ …… + into + 新变量名
* 定类/定序变量通过特定规则，可以转为定距/定比变量

#### 案例（1-1）：将最高教育程度变量"I2_1"转换为最高教育年限
* 第一种方法
```
codebook I2_1.
recode I2_1 (1 = 0)(2 = 6)(3 = 9)(4 thru 7 = 12)(8 = 15)(9 = 16)(10 = 19)(11 = 21)(else = sysmis) into edu_year.
```
* 第二种方法
```
recode I2_1 (1 = 0)(2 = 6)(3 = 9)(4,5,6,7 = 12)(8 = 15)(9 = 16)(10 = 19)(11 = 21)(else = sysmis) into edu_year.
variable labels edu_year "教育年限".
variable level edu_year (scale).
codebook edu_year.
```
* 定距/定比变量通过特定规则，可以转为定类/定序变量

#### 案例（1-2）：将2015年主职总收入变量"I3a_6"转换为收入等级变量
```
codebook I3a_6.
descriptives I3a_6
	/statistics mean stddev min max.
recode I3a_6 (0 thru 6000 = 1)(6000 thru 20000 = 2)(20000 thru 40000 = 3)(40000 thru hi = 4) (else = sysmis) into income_rank.
variable labels income_rank "收入等级".
value labels income_rank 1 "低收入" 2 "中低收入" 3 "中高收入" 4 "高收入".
variable level income_rank (ordinal).
```


#### 2.通过外部公式计算新变量
* 点选操作：转换 -> 计算变量 -> 目标变量（输入新变量名） -> 数字表达式（输入公式） -> 确定
* 语法操作：compute + 新变量名 + "=" + 计算公式

#### 案例（2-1）：将2015年总收入变量"I3a_6"从人民币转换为美元（人民币兑换美元的汇率为6.2284：1）
```
compute income_dollars = I3a_6/6.2284.
compute income_dollars_m = income_dollars/12.
```

#### 随堂练习：计算2015年个人的劳动收入总额，包括主职工资收入（I3a_6_1）和兼职收入（I3b_5）


#### 3.通过变量间关系创建新变量
* 点选操作：转换 -> 计算变量 -> 目标变量（输入新变量名） -> 数字表达式（输入公式）-> 如果（填入运算条件）-> 继续 -> 确定
* 语法操作：
	* if + (变量须满足的条件) + 新变量名 + 赋值
	* 条件包括：&（并）、|（或）

#### 案例（3-1）：创建新变量“性别收入组”，包括男性高收入、男性低收入、女性高收入、女性低收入
* 第一种方法
```
if (gender = 1 & income_rank >= 3) income_sex = 1.
if (gender = 1 & income_rank <= 2) income_sex = 2.
if (gender = 2 & income_rank >= 3) income_sex = 3.
if (gender = 2 & income_rank <= 2) income_sex = 4.
```
* 第二种方法
```
if(gender = 1 & range(I3a_6,20000,max(I3a_6))) income_sex = 1.
if(gender = 1 & range(I3a_6,min(I3a_6),20000)) income_sex = 2.
if(gender = 2 & range(I3a_6,20000,max(I3a_6))) income_sex = 3.
if(gender = 2 & range(I3a_6,min(I3a_6),20000)) income_sex = 4.
variable labels income_sex "性别收入组".
value labels income_sex 1 "男性高收入" 2 "男性高收入" 3 "女性高收入" 4 "女性高收入".
variable level income_sex (nominal).
codebook income_sex.
descriptives income_sex
	/statistics mean stddev min max.
```

#### 随堂练习：利用大学（I2_1）和收入（I3a_6）变量，创建新变量“学历收入组”，包括大学高收入、大学低收入、非大学高收入、非大学低收入.


#### 4.通过组间关系创建新变量
* 点选操作为：数据 -> 汇总 -> 分界变量（选择分组变量） -> 汇总变量（选择用以计算的变量） -> 函数（选择摘要统计）
-> 继续 -> 变量与标签 -> 确定
* 语法操作为：aggregate + /break（分组变量） + /新变量名 + "新变量标签" + /函数

#### 案例（4-1）：计算2015年家庭的总收入
* 注意：
	* 问卷编号"IID2016"为受访者的个人唯一识别码，即任意两人不相等
	* 问卷编号"FID2016"为受访者的家庭唯一识别码，即同一家庭内的受访者相等，但不同家庭的受访者不相等
	* 将同一家庭内不同个体总收入"I3a_6"进行加总
```
aggregate 
	/break=FID2016 
	/household_income "家庭总收入" = sum(I3a_6).
```

#### 案例（4-2）：如何计算2015年家庭人口规模
* 第一种方法
```
compute member = 1.
aggregate 
	/break=FID2016 
	/household_num "家庭总收入" = sum(member).
```
* 第二种方法
```
aggregate 
	/break=FID2016 
	/household_num = n.
```

#### 随堂练习：计算2015年家庭总劳动投入，用工作时间（I3a_1）来衡量


#### 5.变量的筛选
* 点选操作为：数据 -> 选择个案 -> 条件栏（选"如果条件满足"，点击"如果"）-> 选择变量 -> 在空白框输入条件（"|"指"或"，"&"指"并"）
-> 继续 -> 输出框（选"删除未选定的个案"） -> 确定
* 语法操作为：select if + 条件或函数
* 注意：变量筛选前必须先检查异常值分布

#### 案例（5-1）：查看中国女性劳动者的2015年的收入状况
* 分析背景：
	* 根据OECD统计口径，筛选年龄为18~65岁的劳动人口（具备潜在劳动能力）
	* 根据性别状况，筛选女性人口
	* 根据收入状况，筛选有收入的人口
* 第一步：查看性别与出生年份变量的异常值分布
```
codebook gender birthyear.
descriptives gender birthyear
	/statistics mean stddev min max.
```
* 第二步：对出生年份异常值进行处理
```
recode birthyear (2016 thru hi = sysmis)(else = copy).
```
* 第三步：计算人口年龄
```
compute age = 2015 - birthyear.
variable labels age "年龄".
variable level age (scale).
descriptives age
	/statistics mean stddev min max.
```
* 第四步：筛选劳动年龄、女性和有收入的人口
```
select if ((age  >= 18 & age <= 65) & gender = 2 & I3a_6 > 0).
descriptives I3a_6
	/statistics mean stddev min max.
```

#### 随堂练习：查看中国老年劳动者的2015年收入状况
* 注意：
	* 筛选65岁以上人口
	* 筛选有工作的人口（work2015）


#### 6.变量的排序与排秩
* 点选操作为：
	* 数据 -> 个案排序 -> 选择变量（选至排序依据） -> 排序依据 -> 确定
	* 转换 -> 个案排秩 -> 选择变量 -> 类型排秩（选"秩"或"百分比秩"）-> 继续
* 语法操作为：
	* sort cases by + 变量 + (a)/(d)（升序或降序）
	* rank variable = + 变量 + (a)/(d)（升序或降序） + /rank（秩）或 /percent（百分比秩）

#### 案例（6-1）：查看女性受访者收入在全体女性劳动者中的排名与排秩
* 第一种方法：直接查看女性劳动收入排名
```
recode I3a_6 (sysmis = 0)(else = copy) into income.
sort cases by income (a).
```
* 第二种方法：创建一个新变量，显示女性劳动收入排名
```
compute sort_no = $casenum.
```

#### 案例（6-2）：查看女性受访者收入在全体女性劳动者中的排秩
* 创建一个新变量，显示女性劳动收入排秩和百分比
```
rank variables =income (a)
	/rank.
rank variables =income (a)
	/percent.
```

#### 随堂练习：计算收入前10%群体的收入总和占总体收入的比例，以此作为衡量社会不平等的指标


#### 7.探索性分析
* 点选操作为：分析 -> 描述报告 -> 探索 -> 填入因变量列表 -> 子因变量列表（分组变量） -> 选择统计量和图 -> 确定
* 语法操作为：examine variables =  + 变量名 + by 分组变量 + /plot（图形类型） + /statistics（统计量）
	* /plot（图形类型）：histograms（直方图）、boxplot（箱型图）、stemleaf（茎叶图）、npplot（正态分布图）
	* /statistics（统计量）：descriptives（全样本描述统计量）、extreme（极端值描述统计量）、none（不输出描述统计量）

#### 案例（7-1）：探索性别收入不平等
* 第一步：查看性别
```
codebook gender.
descriptives gender
	/statistics mean stddev min max.
recode gender (1 = 1)(2 = 0) into male.
variable labels male "性别（男性）".
value labels male 1 "男性" 0 "女性".
variable level male (nominal).
codebook male.
descriptives male
	/statistics mean stddev min max.
```
* 第二步：查看2015年主职总收入信息
```
codebook I3a_6.
descriptives I3a_6
	/statistics mean stddev min max.
compute total_income = I3a_6.
variable labels total_income "2015主职总收入".
variable level total_income (scale).
```
* 第三步：描述性别和2015年主职总收入
```
examine variables = gender total_income.
examine variables = total_income by gender
	/plot histograms  
	/statistics descriptives.
examine variables = total_income by gender
	/plot histograms  
	/statistics descriptives.
```

#### 随堂练习：比较不同劳动年龄段总收入情况（按18-35岁、36-50岁、51-65岁划分）


#### 8.描述变量频率
* 点选操作为：分析 -> 描述统计 -> 频率 -> 填入统计量或图表 -> 继续 -> 确定
* 语法操作为：frequencies variables = + 变量 + 如下可选项
	* /format（格式）可选内容：avalue（变量取值升序）、devalue（变量取值降序）、afreq（变量数目升序）、dfreq（变量数目降序）
	* /barchart（条形图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /piechart（饼状图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /historgam（直方图）可选内容：min(n)（排除最小值）、max(n)（排除最大值）、freq（显示频率）、percent（显示百分比）
	* /statistics（统计量）可选内容：mean（均值）、semean（标准误均值）、median （中位数）、mode（众数）、stddev（标准差）、variance（方差）、 skewness（峰度）、 kurtosis（峰态）、range（取值范围）、 minimum（最小值）、 maximum（最大值）、 sum（总和）
	* /percentiles（分位数）：可填入分位数，用","隔开
* 注意：/statistics还可选default（默认mean、stddev、minimum、maximum）、all（全部）或none（无）

#### 案例：将2015年主职总收入划分等级
* 第一步：将定距型收入变量重新编码为定序型收入变量
```
recode I3a_6 (0 thru 6000 = 1)(6000 thru 20000 = 2)(20000 thru 40000 = 3)(40000 thru hi = 4) (else = sysmis) into income_rank.
variable labels income_rank "收入等级".
value labels income_rank 1 "低收入" 2 "中低收入" 3 "中高收入" 4 "高收入".
variable level income_rank (ordinal).
```
* 第二步：用条形图和饼状图描述2015年主职总收入等级
```
frequencies variables = income_rank
	/barchart freq.
frequencies variables = income_rank
	/piechart percent.
```
* 第三步：描述2015年主职总收入统计量
```
frequencies variables = I3a_6
	/statistics mean semean median mode stddev variance skewness kurtosis range minimum maximum sum
	/percentiles = 5,25,50,75,95.
```

* 随堂练习：描述2015年平均每周工作时间（I3a_1），并将其划分为不充分就业（小于35小时）、正常就业（35-45小时）、过劳就业（超过45小时）


#### 9.列联表分析
* 点选操作为：分析 -> 描述统计 -> 交叉表 -> 填入"行"或"列" -> 填入统计量等 -> 继续 -> 确定
* 语法操作为：crosstabs + /tables = 行变量 + by + 列变量 + by + 层内变量…… + 如下可选项 
	* /cell（单元格信息）：count（计数）、row（按行计算百分比）、column（按列计算百分比）、total（行列综合百分比）、expected（期望值）、none（不显示）
	* /statistics（统计量）：chisq（卡方值）、phi（卡莱默系数）、cc（相依系数）、lambda（lambda系数）、gamma（gamma系数）、corr（皮尔逊相关系数）
	* /missing（报告缺失值）：include（包含在列联表内）、report（直接汇报）

#### 案例（9-1）：分析党员身份与收入等级关系
* 第一步：对党员身份进行重新编码
```
codebook I1_6.
descriptives I1_6
	/statistics mean stddev min max.
recode I1_6 (1 = 1) (2 thru 3 = 0) (else = sysmis) into be_ccp.
variable labels be_ccp "政治面貌（党员）".
value labels be_ccp 1 "党员" 0 "非党员".
variable level be_ccp (nominal).
```
* 第二步：描述党员身份与主职收入
```
crosstabs
	/tables = income_rank by be_ccp.
crosstabs
	/tables =  be_ccp by income_rank.
crosstabs
	/tables =  be_ccp by income_rank by gender.
crosstabs 
	/tables=income_rank by be_ccp 
	/cells = column
	/statistics = chisq.
```

#### 随堂练习：描述政治面貌、劳动年龄与总收入的关系
* 注意：
	* 政治面貌为：党员、非党员
	* 劳动年龄为：青壮年（18-35岁）、中年（36-50岁）、中老年（51-65岁）
	* 总收入包括：2015年主职收入和兼职收入，按照4个分位划分为4个等级

---
date: "2021-08-01T13:05:05-05:00"
tags:
- 推断统计
title: 第九节 抽样分布、区间估计与总体均值检验
---


#### 0.预处理
* 设置工作目录（working directory）
```
cd "F:/SPSS".
```
* 导入2016年中国劳动力动态调查数据
```
get file "clds2016_i.sav".
dataset names clds window = front.
dataset activate clds.
```


#### 1.计算总体、样本及抽样分布的取值
#### 案例（1-1）：以2015年主职总收入为总体，计算总体、样本及抽样分布取值
* 从原始数据中抽取规模为500的样本
* 分别计算总体、样本和抽样分布的均值、方差和标准差
* 计算总体均值、方差和标准差
```
select if (I3a_6  >= 0).
compute income = I3a_6.
aggregate 
	/pop_mean = mean(income).
compute diff_sq = (income - pop_mean)**2.
aggregate 
	/pop_var = mean(diff_sq).
compute pop_sd = sqrt(pop_var).
formats pop_mean pop_var pop_sd (f20.16).
descriptives pop_mean pop_var pop_sd
	/statistics mean.
```
* 计算样本均值、方差和标准差.
```
sample 500 from 1462.
aggregate 
	/sam_mean = mean(income).
compute sam_diff_sq = (income - sam_mean)**2.  
aggregate 
	/sam_var = mean(sam_diff_sq).
compute sam_sd = sqrt(sam_var).
formats sam_mean sam_var sam_sd (f20.16).
descriptives sam_mean sam_var sam_sd
	/statistics mean.
```
* 计算抽样分布的均值、方差和标准差
* 若总体已知
```
compute sampling_mean_pop = pop_mean.
compute sampling_var_pop = pop_var/500.
compute sampling_sd_pop = sqrt(sampling_var_pop).
formats sampling_var_pop sampling_sd_pop (f20.16).
descriptives sampling_mean_pop sampling_var_pop sampling_sd_pop
	/statistics mean.
```
* 若总体未知
* 第一种方法：根据统计公式直接进行计算
* 注意：进行贝塞尔矫正的样本方差才是总体方差的无偏估计
```
compute sampling_mean_sam1 = sam_mean.
aggregate 
	/sam_diff_sum = sum(sam_diff_sq). 
compute sam_var_corr = sam_diff_sum/(500-1).
compute sam_sd_corr = sqrt(sam_var_corr).
compute sampling_var_sam1 = sam_var_corr/500.
compute sampling_sd_sam1 = sqrt(sampling_var_sam1).
formats sampling_var_sam1 sampling_sd_sam1 (f20.16).
descriptives sampling_mean_sam1 sampling_var_sam1 sampling_sd_sam1
	/statistics mean.
```
* 第二种方法：利用SPSS自带命令与输出管理功能进行计算
* 注意：
	* 用语法descriptives计算，输出结果已进行贝塞尔矫正
	* 用输出管理系统（Output Management System，OMS）将输出结果转为数据
		* 输出管理系统是SPSS的进阶数据管理功能，可以系统管理所有数据输出结果
		* 使用输出管理系统时，建议将SPSS输出界面设置为英语，以使生成数据的变量名为英语，设置方式为：编辑 -> 选项 -> 语言 -> 输出栏选“英语”；
		* 基本语法结构包含两部分，其一为“oms……omsend.”，其二为“其他统计语法”，后者嵌套在前者内，具体结构为
```
oms 
/select 选择输出内容（charts图、headings标题、logs日志、models模型、tables表、texts文本、trees支线、warnings警告或 all expect全部除了= [list]）
/if commands = ["涵盖的语法（语法1、语法2……）"]
    subtypes = ["具体输出的子内容"……]
/destination format = 输出文件格式（html、pdf、text、sav、spv、xls、xlsx……） outfile = 输出文件名. （注意：此处有休止符“.”）
语法1 ……
语法2 ……
omsend.
```
* 将语法descriptives计算income的结果存储为数据
```
oms 
	/select tables
	/if commands = ["descriptives"] subtypes = ["descriptive statistics"]
	/destination format = sav outfile = "sample_descriptives.sav".
descriptives variables = income 
	/statistics=mean stddev variance min max semean.
omsend.
```
* 读取新数据，读取时仅保留统计量
```
get file "sample_descriptives.sav"
	/keep = N_Statistic to Variance_Statistic.
dataset names clds window = front.
dataset activate clds.
```
* 查看新数据所有变量的基本信息
```
display dictionary.
```
* 对新数据的所有变量重新命名
```
rename variables (all = n min max mean mean_se sd var).
```
* 利用筛选功能，保留第一行
```
compute case_num = $casenum.
select if (case_num = 1).
```
* 创建抽样分布的均值、方差和标准差
```
compute sampling_mean_sam2 = mean.
compute sampling_sd_sam2 = mean_se.
compute sampling_var_sam2 = sampling_sd_sam2**2.
formats sampling_var_sam1 sampling_sd_sam1 (f20.16).
descriptives sampling_mean_sam2 sampling_var_sam2 sampling_sd_sam2
	/statistics mean.
```
* 随堂练习：以生活幸福感（I7_6_1）作为总体，随机抽取400个样本（随机种子为20200501,总体情况未知），分别计算总体、样本和抽样分布的均值、方差和标准差


#### 2.根据样本信息，对总体进行区间估计
* 承接上文抽样背景，计算2015年主职总收入的99%的置信区间
* 第一种方法：根据统计公式直接进行区间估计
```
dataset activate clds.
compute interval_lower = sampling_mean_sam1 - 2.58*sampling_sd_sam1.
compute interval_upper = sampling_mean_sam1 + 2.58*sampling_sd_sam1.
descriptives sampling_mean_sam1 interval_lower interval_upper
	/statistics mean.
```
* 第二种方法：利用语法examine进行区间估计
```
examine variables = income
	/plot none
	/cinterval 99.
```
* 第三种方法：利用拔靴法修正的抽样误差进行区间估计
	* 点选操作为：分析 -> 描述统计 -> 探索 -> 在左侧变量列表中选择变量 -> 选择统计 -> 勾选描述并填入置信区间 -> 继续
-> 自助抽样 -> 勾选执行自助抽样（填入样本数小于原始样本数） -> 填入置信区间 -> 继续 -> 确定；
	* 语法结构包含两部分，其一为拔靴法设置语法，其二为区间估计语法，具体为：
bootstrap + /variables target = + “变量名” + /criteria cilevel = + “置信区间” + nsamples = “重复抽样中的样本规模” .
* 用拔靴法计算2015年主职总收入的99%的置信区间.
```
bootstrap 
	/variables target = income 
	/criteria cilevel = 99 nsamples = 400.
examine variables = income
	/plot none
	/cinterval 99.
```
* 随堂练习：以生活幸福感（I7_6_1）作为总体，随机抽取400个样本（随机种子为20200501,总体情况未知），计算95%置信区间（Z值为1.96）


#### 3.以抽样分布的标准差作为不同图层，绘制抽样分布图和区间分布图
* 注意：以下为Stata命令
* 绘制抽样分布图
* 原始抽样分布
```
local path "F:/General Database of Social Science/Courses/prepare for course/Statistics Software for Social Science II"
#delimit;
graph twoway 
	(function y=normalden(x,30123.83584,1147.37221), range(25000 35000)lw(medthin)color(gs16) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(31271.20805 32418.58026)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(32418.58026 33565.95247)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(33565.95247 34713.32468)lw(medthin)color(gs0) recast(area))	
	(function y=normalden(x,30123.83584,1147.37221), range(27829.09142 28976.46363)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(26681.71921 27829.09142)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(25534.34700 26681.71921)lw(medthin)color(gso) recast(area))	
	(function y=normalden(x,30123.83584,1147.37221), range(25000 35000)lw(medthin)lpattern(solid)),
	title("Normal Distribution for Income")
	xtitle("income(sample mean)", size(medlarge)) ytitle("density")
	legend(off) 
	xlabel(25534 26682 27829 28976 30124 31271 32419 33566 34713, grid gmin gmax);
graph export "`path'\Week 10\1.normal_income(with notation).png", as(png) width(4000) replace;
graph twoway 
	(function y=normalden(x,30123.83584,1147.37221), range(25000 35000)lw(medthin)color(gs16) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(31271.20805 32418.58026)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(32418.58026 33565.95247)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(33565.95247 34713.32468)lw(medthin)color(gs0) recast(area))	
	(function y=normalden(x,30123.83584,1147.37221), range(27829.09142 28976.46363)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(26681.71921 27829.09142)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x,30123.83584,1147.37221), range(25534.34700 26681.71921)lw(medthin)color(gso) recast(area))	
	(function y=normalden(x,30123.83584,1147.37221), range(25000 35000)lw(medthin)lpattern(solid)),
	title("Normal Distribution for Income")
	xtitle("income(sample mean)", size(medlarge)) ytitle("density")
	legend(off) 
	xlabel(25534.34700 "-4 sd" 26681.71921 "-3 sd" 27829.09142 "-2 sd" 28976.46363 "-1 sd" 30123.83584 "mean" 31271.20805 "1 sd" 32418.58026 "2 sd" 33565.95247 "3 sd " 34713.32468 "4 sd", grid gmin gmax);
graph export "`path'\Week 10\2.normal_income(without notation).png", as(png) width(4000) replace;
#delimit cr
```
* 标准正态分布
```
#delimit;
graph twoway 
	(function y=normalden(x), range(-4 4)lw(medthin)color(gs16) recast(area))
	(function y=normalden(x), range(1 2)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x), range(2 3)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x), range(3 4)lw(medthin)color(gs0) recast(area))	
	(function y=normalden(x), range(-2 -1)lw(medthin)color(gs13) recast(area))
	(function y=normalden(x), range(-3 -2)lw(medthin)color(gs9) recast(area))
	(function y=normalden(x), range(-4 -3)lw(medthin)color(gso) recast(area))	
	(function y=normalden(x), range(-4 4)lw(medthin)lpattern(solid)),
	title("Standard Normal Distribution for Income")
	xtitle("income(sample mean)", size(medlarge)) ytitle("density")
	legend(off) 
	xlabel(-4 "-4 sd" -3 "-3 sd" -2 "-2 sd" -1 "-1 sd" 0 "0" 1 "1 sd" 2 "2 sd" 3 "3 sd " 4 "4 sd", grid gmin gmax);
graph export "`path'\Week 10\3.standard normal_income(with notation).png", as(png) width(4000) replace;
#delimit cr
```
* 绘制区间分布图
```
cd "`path'\data"
use clds2016_i.dta,clear
drop if I3a_6 == . | I3a_6 < 0
rename I3a_6 income
foreach i of numlist 1(1)100 {
	preserve
	set seed `i'0430
	sample 500,count
	ci mean income,level(95)
	gen income_lower = r(lb)
	gen income_upper = r(ub)
	keep if _n == 1
	keep income_lower income_upper
	sxpose,clear force
	destring _var1,replace
	rename _var1 interval_`i'
	gen id = _n
	gen id_`i' = `i'
	sa "simulation4/income_`i'.dta",replace
	restore
}

cd "`path'\data\simulation4"
use income_1.dta,clear
foreach i of numlist 2(1)100 {
	merge 1:1 id using income_`i'.dta,nogen
}


sa income_all.dta,replace
```

* 将重复抽样结果绘制在同一张图上
```
use income_all,clear
local c "connect"
local v "interval"
local s "msymbol(pipe)"
local l "lw(medthick)"
#delimit;
twoway (`c' id_1 `v'_1,`s'`l')(`c' id_2 `v'_2,`s'`l')
       (`c' id_3 `v'_3,`s'`l')(`c' id_4 `v'_4,`s'`l')
	   (`c' id_5 `v'_5,`s'`l')(`c' id_6 `v'_6,`s'`l'),
		xline(30123.83584131327,lpattern(dash))legend(off) 
		title("Confident Intervals for Total Income(n = 500)")
		xtitle("confident interval for repeat sampling(𝛼 = 0.05)") ytitle("sampling No.")
		xlabel(15000(5000)45000)ylabel(0(1)6);
graph export "`path'\Week 10\4.confident interval.png", as(png) width(4000) replace;
twoway (`c' id_1 `v'_1,`s')(`c' id_2 `v'_2,`s')
       (`c' id_3 `v'_3,`s')(`c' id_4 `v'_4,`s')
	   (`c' id_5 `v'_5,`s')(`c' id_6 `v'_6,`s')
	   (`c' id_7 `v'_7,`s')(`c' id_8 `v'_8,`s')
	   (`c' id_9 `v'_9,`s')(`c' id_10 `v'_10,`s')
	   (`c' id_11 `v'_11,`s')(`c' id_12 `v'_12,`s')
       (`c' id_13 `v'_13,`s')(`c' id_14 `v'_14,`s')
	   (`c' id_15 `v'_15,`s')(`c' id_16 `v'_16,`s')
	   (`c' id_17 `v'_17,`s')(`c' id_18 `v'_18,`s')
	   (`c' id_19 `v'_19,`s')(`c' id_20 `v'_20,`s')
	   (`c' id_21 `v'_21,`s')(`c' id_22 `v'_22,`s')
       (`c' id_23 `v'_23,`s')(`c' id_24 `v'_24,`s')
	   (`c' id_25 `v'_25,`s')(`c' id_26 `v'_26,`s')
	   (`c' id_27 `v'_27,`s')(`c' id_28 `v'_28,`s')
	   (`c' id_29 `v'_29,`s')(`c' id_30 `v'_30,`s')
	   (`c' id_31 `v'_31,`s')(`c' id_32 `v'_32,`s')
       (`c' id_33 `v'_33,`s')(`c' id_34 `v'_34,`s')
	   (`c' id_35 `v'_35,`s')(`c' id_36 `v'_36,`s')
	   (`c' id_37 `v'_37,`s')(`c' id_38 `v'_38,`s')
	   (`c' id_39 `v'_39,`s')(`c' id_40 `v'_40,`s')
	   (`c' id_41 `v'_41,`s')(`c' id_42 `v'_42,`s')
       (`c' id_43 `v'_43,`s')(`c' id_44 `v'_44,`s')
	   (`c' id_45 `v'_45,`s')(`c' id_46 `v'_46,`s')
	   (`c' id_47 `v'_47,`s')(`c' id_48 `v'_48,`s')
	   (`c' id_49 `v'_49,`s')(`c' id_50 `v'_50,`s')
	   (`c' id_51 `v'_51,`s')(`c' id_52 `v'_52,`s')
       (`c' id_53 `v'_53,`s')(`c' id_54 `v'_54,`s')
	   (`c' id_55 `v'_55,`s')(`c' id_56 `v'_56,`s')
	   (`c' id_57 `v'_57,`s')(`c' id_58 `v'_58,`s')
	   (`c' id_59 `v'_59,`s')(`c' id_60 `v'_60,`s')
	   (`c' id_61 `v'_61,`s')(`c' id_62 `v'_62,`s')
       (`c' id_63 `v'_63,`s')(`c' id_64 `v'_64,`s')
	   (`c' id_65 `v'_65,`s')(`c' id_66 `v'_66,`s')
	   (`c' id_67 `v'_67,`s')(`c' id_68 `v'_68,`s')
	   (`c' id_69 `v'_69,`s')(`c' id_70 `v'_70,`s')
	   (`c' id_71 `v'_71,`s')(`c' id_72 `v'_72,`s')
       (`c' id_73 `v'_73,`s')(`c' id_74 `v'_74,`s')
	   (`c' id_75 `v'_75,`s')(`c' id_76 `v'_76,`s')
	   (`c' id_77 `v'_77,`s')(`c' id_78 `v'_78,`s')
	   (`c' id_79 `v'_79,`s')(`c' id_80 `v'_80,`s')
	   (`c' id_81 `v'_81,`s')(`c' id_82 `v'_82,`s')
       (`c' id_83 `v'_83,`s')(`c' id_84 `v'_84,`s')
	   (`c' id_85 `v'_85,`s')(`c' id_86 `v'_86,`s')
	   (`c' id_87 `v'_87,`s')(`c' id_88 `v'_88,`s')
	   (`c' id_89 `v'_89,`s')(`c' id_90 `v'_90,`s')
	   (`c' id_91 `v'_91,`s')(`c' id_92 `v'_92,`s')
       (`c' id_93 `v'_93,`s')(`c' id_94 `v'_94,`s')
	   (`c' id_95 `v'_95,`s')(`c' id_96 `v'_96,`s')
	   (`c' id_97 `v'_97,`s')(`c' id_98 `v'_98,`s')
	   (`c' id_99 `v'_99,`s')(`c' id_100 `v'_100,`s'),
		xline(30123.83584131327,lpattern(dash))legend(off) 
		title("Confident Intervals for Total Income(n = 500)")
		xtitle("confident interval for repeat sampling(𝛼 = 0.05)") ytitle("sampling No.")
		xlabel(15000(5000)45000)ylabel(0(10)100);
graph export "`path'\Week 10\5.confident interval.png", as(png) width(4000) replace;
#delimit cr
```

#### 4.编制均值表
* 均值表可以提供变量的基本统计信息
* 点选操作为：分析 -> 比较平均值 -> 平均值 -> 在左侧变量列表中选择变量（若分组，在层中选择分组变量） -> 点击“选项” -> 选择统计量 -> 继续 -> 可选自助抽样 -> 勾选执行自助抽样（填入样本数小于原始样本数） -> 填入置信区间 -> 继续 -> 确定
* 语法结构为：means + 变量名（若分组，则再加上“by 分组变量名”） + /cells = + 统计量（mean均值、count个数、var方差、stddev标准差、semean均值标准误差、min最小值、max最大值）

#### 案例（4-1）：编制2015年主职总收入均值表
* 导入2016年中国劳动力动态调查数据和构造income变量
```
get file "clds2016_i.sav".
execute.
dataset names clds window = front.
dataset activate clds.
select if (I3a_6  >= 0).
compute income = I3a_6.
```
* 查看2015年主职总收入的均值、样本量、方差和标准差
```
means income
	/cells = mean count var stddev.
```
* 按性别分组描述
```
means income by gender
	/cells = mean count var stddev.
```
* 均值表附加功能：用拔靴法计算置信区间
```
bootstrap 
	/variables target = income 
	/criteria cilevel = 99 nsamples = 1400.
means income
	/cells = mean count var stddev semean min max.
```
#### 随堂练习：按性别分组呈现最高教育程度（I2_1）的基本统计信息


#### 5.基于调查数据的单样本总体均值检验
* 单样本的总体均值检验是对“根据样本信息，判断总体均值是否等于某一确定取值”的假设检验.
* 点选操作为：分析 -> 比较平均值 -> 单样本T检验 -> 在左侧变量列表中选择检验变量 -> 在“检验值”中填入总体均值 -> 点击“选项” -> 填入置信区间 -> 继续 -> 可选自助抽样 -> 勾选执行自助抽样（填入样本数小于原始样本数） -> 填入置信区间 -> 继续 -> 确定
* 语法结构为：t-test + /testval = + 总体均值 + /variables = + 待检验变量 + /criteria = + ci(“1-α”取值)

#### 案例（5-1）：根据样本信息，在置信区间为99%的前提下，判断2015年主职总收入的总体均值是否等于25000元。请写出虚无假设、研究假设和检验过程
* 虚无假设H0：2015年主职总收入（的总体均值）等于25000元
* 研究假设H1：2015年主职总收入（的总体均值）不等于25000元

#### 注意：此节已将2016年中国劳动力动态调查数据当作样本，与前文模拟抽样时当作总体有本质区别
#### 第一种方法：根据统计公式直接进行计算
* 第一种方法又可以通过三种方式判断
* 第一种方式：通过Z值判断（若Z值大于2.576，则拒绝原假设）
	* 第一步：计算样本均值
```
aggregate 
	/sam_income_mean = mean(income).
```

	* 第二步：计算样本离差平方
```
compute diff_sq = (income - sam_income_mean)**2.
```

	* 第三步：计算样本离差平方和
```
aggregate 
	/diff_sq_sum = sum(diff_sq).
```

	* 第四步：计算总体方差（经过贝塞尔校正）
```
compute income_var = diff_sq_sum/(1462-1).
```

	* 第五步：计算总体标准差（经过贝塞尔校正）
```
compute income_sd = sqrt(income_var).
```

	* 第六步：计算抽样分布标准差
```
compute income_se = income_sd/sqrt(1462).
```

	* 第七步：设定虚无假设正确时的总体均值
		* 若虚无假设正确，总体均值为25000
```
compute pop_income_mean = 25000.
```

	* 第八步：计算Z值（以抽样分布的标准差为单位，度量样本均值到总体均值的距离）
```
compute Z_score = (sam_income_mean-pop_income_mean)/income_se.
formats Z_score (f20.16).
descriptives Z_score
	/statistics mean.
```
* 可知实际Z值（4.464186）大于99%置信区间的Z值（2.576），拒绝虚无假设，接受研究假设.

* 第二种方式：通过区间估计值判断（若99%置信区间下得到的区间估计值不包含25000，则拒绝虚无假设）
	* 这里第一至六的步骤同第一种方式
	* 第七步：在99%置信区间下，估计总体均值的区间上限和区间下限
```
compute income_upper = sam_income_mean+2.576*income_se.
compute income_lower = sam_income_mean-2.576*income_se.
formats income_lower income_upper (f20.16).
descriptives income_lower income_upper
	/statistics mean.
```
* 可知区间下限为27167.193698，不包含25000，拒绝虚无假设，接受研究假设

* 第三种方式：通过p值判断（在99%置信区间下，p值若小于α即0.01，则拒绝虚无假设）
```
formats sam_income_mean pop_income_mean income_se(f20.16).
descriptives sam_income_mean pop_income_mean income_se
	/statistics mean.
```
* 尽管大样本（n ≥ 30）的抽样分布为正态分布，但SPSS软件的总体均值检验默认将正态分布视作t分布的特例
* 根据t分布特性，在大样本情况下,t分布近似标准正态分布；随着样本规模增加，t分布越近似标准正态分布
* 因此，总体均值检验可以利用正态分布特性，也可以利用t分布特性
	* 在正态分布下，P值等于整个正态分布的累积密度（面积=1）与样本均值处的累积密度（面积 = cdf.normal(样本均值,总体均值,抽样分布标准差)或cdf.normal(实际Z值,0,1)）之差的2倍
	* 在t分布下，P值等于整个t分布的累积密度（面积=1）与样本均值处的累积密度（面积 = cdf.t(实际Z值,自由度)）之差的2倍
```
compute p_value_normal_1 = (1 - cdf.normal(30123.835841,25000,1147.764807007452400))*2.
compute p_value_normal_2 = (1 - cdf.normal(4.464186,0,1))*2.
compute p_value_t = (1 - cdf.t(4.464186,1461))*2.
formats p_value_normal_1 p_value_normal_2 p_value_t (f20.16).
descriptives p_value_normal_1 p_value_normal_2 p_value_t
	/statistics mean.
```
* 可知p值（=0.000008037或0.000008654714756）小于0.01，拒绝虚无假设，接受研究假设

#### 第二种方法：利用语法t-test进行计算
```
t-test
	/testval = 25000
	/variables = income
	/criteria = ci(0.99).
```
* 可知p值（=0.000009）小于0.01，拒绝虚无假设，接受研究假设

#### 第三种方法：采用拔靴法（从已有1462个样本不断重复抽取1400个样本）修正抽样分布标准误后，再进行假设检验
```
bootstrap 
	/variables target = income 
	/criteria cilevel = 99 nsamples = 1400.
t-test
	/testval = 25000
	/variables = income
	/criteria = ci(0.99).
```
* 可知p值（=0.000714）小于0.01，拒绝虚无假设，接受研究假设

#### 随堂练习：试检验2015年中国劳动者的平均最高教育程度（I2_1）是否等于8年，置信区间为95%


#### 6.基于调查数据的独立样本总体均值检验
* 点选操作为：分析 -> 比较平均值 -> 独立样本T检验 -> 选择检验变量 -> 选择分组变量（定义比较组时，填入变量取值）-> 选项（填入置信区间） -> 继续 -> 可选自助抽样 -> 勾选执行自助抽样（填入样本数小于原始样本数） -> 填入置信区间 -> 继续 -> 确定
* 语法结构为：t-test groups = + 分组变量（代表不同组的变量取值）+ /variables = 检验变量 + /criteria = ci(置信区间)

#### 案例（6-1）：根据样本信息，在置信区间为99%的前提下，判断2015年中国男性和女性主职总收入的总体均值是否相等
* 请写出虚无假设、研究假设和检验过程
	* 虚无假设H0：男性和女性的2015年主职总收入（的总体均值）相等（即两者均值之差为0）
	* 研究假设H1：男性和女性的2015年主职总收入（的总体均值）不相等（即两者均值之差不为0）

#### 第一种方法：在大样本（n ≥ 30）情况下，根据统计公式直接进行计算，包含三种方式
* 第一种方式：通过Z值判断（若Z值大于2.576，则拒绝原假设）
	* 第一步：查看男性样本和女性样本2015年主职总收入的个案数、均值、方差和标准差
```
means tables = income by gender
	/cells = count mean var stddev.
```
	* 第二步：以男女样本均值之差作为总体均值之差的无偏估计
```
compute male_mean = 34905.0726.
compute female_mean = 24134.397535.
compute diff_mean = male_mean - female_mean.
```
	* 第三步：计算男女样本均值之差所构成的抽样分布的方差
```
compute male_n = 813.
compute female_n = 649.
compute male_var = 2505693660.582166.
compute female_var =  1137925108.002217.
compute diff_var = male_var/male_n + female_var/female_n.
compute diff_se = sqrt(diff_var).
```
	* 第四步：计算Z值（以抽样分布的标准差为单位，度量男女样本均值之差到总体均值之差即0的距离）
```
compute Z_score_diff = (diff_mean-0)/diff_se.
formats Z_score_diff (f20.16).
descriptives Z_score_diff
	/statistics mean.
```
* 可知实际Z值（4.898096895558914）大于99%置信区间的Z值（2.576），拒绝虚无假设，接受研究假设

* 第二种方式：通过区间估计值判断（若99%置信区间下得到的区间估计值不包含0，则拒绝虚无假设）
	* 这里第一、二步同第一种方式
	* 第三步：在99%置信区间下，估计男女收入总体差值之均值的区间上限和区间下限
```
compute diff_upper = diff_mean+2.576*diff_se.
compute diff_lower = diff_mean-2.576*diff_se.
formats diff_lower diff_upper (f20.16).
descriptives diff_lower diff_upper
	/statistics mean.
```	
* 可知区间下限为5106.177290，不包含0，拒绝虚无假设，接受研究假设

* 第三种方式：通过p值判断（在99%置信区间下，p值若小于α即0.01，则拒绝虚无假设）
```
formats diff_mean diff_se Z_score_diff(f20.16).
descriptives diff_mean diff_se Z_score_diff
	/statistics mean.
compute p_normal_diff_1 = (1 - cdf.normal(10770.675065,0,2198.951000))*2.
compute p_normal_diff_2  = (1 - cdf.normal(4.898096895558914,0,1))*2.
formats p_normal_diff_1 p_normal_diff_2 (f20.16).
descriptives p_normal_diff_1 p_normal_diff_2
	/statistics mean.
```
* 可知p值（=0.000000967693164）小于0.01，拒绝虚无假设，接受研究假设


* 若认为抽样分布为t分布，则假设检验须考虑两种情形
* 第一种情形：认为男性和女性2015年主职总收入具有相同的方差
	* 由于每一种情形也具有三种判断方式，以下仅以p值为例
	* 第一步：计算男女总体收入之差的方差的混合估计量（variance of pooled estimator）
```
compute pooled_var = ((male_n-1)*male_var + (female_n-1)*female_var)/(male_n+female_n-2).
compute pooled_sd = sqrt(pooled_var).
```
	* 第二步：计算男女总体收入之差的抽样分布的标准差
```
compute sqrt_n = sqrt((1/male_n)+(1/female_n)).
compute pooled_se = pooled_sd*sqrt_n.
```
	* 第三步：计算Z值
```
compute Z_pooled_diff = (diff_mean-0)/pooled_se.
```
	* 第四步：通过p值判断（在99%置信区间下，p值若小于α即0.01，则拒绝虚无假设）
```
formats Z_pooled_diff (f20.16).
descriptives Z_pooled_diff
	/statistics mean.
```
	* 注意：此时自由度=男性样本数+女性样本数-2
```
compute p_diff_t_1 = (1 - cdf.t(4.695877661100175,1460))*2.
formats p_diff_t_1 (f20.16).
descriptives p_diff_t_1
	/statistics mean.
```
* 可知p值（=0.000002904076144）小于0.01，拒绝虚无假设，接受研究假设

* 第二种情形：认为男性和女性2015年主职总收入具有相同的方差
* 以下仅以p值为例
	* 第一步：计算抽样分布方差和Z值
```
compute diff_var = male_var/male_n + female_var/female_n.
compute diff_se = sqrt(diff_var).
compute Z_score_diff = (diff_mean-0)/diff_se.
```
	* 第二步：计算自由度，取四舍五入值
```
compute df_diff = (diff_var**2)/((((male_var/male_n)**2)/(male_n-1))+(((female_var/female_n)**2)/(female_n-1))).
```
	* 第三步：通过p值判断（在99%置信区间下，p值若小于α即0.01，则拒绝虚无假设）
```
formats Z_score_diff df_diff (f20.16).
descriptives Z_score_diff
	/statistics mean.
compute p_diff_t_2 = (1 - cdf.t(4.898096895558914,1422))*2.
formats p_diff_t_2 (f20.16).
descriptives p_diff_t_2
	/statistics mean.
```
* 可知p值（=0.000001078383088）小于0.01，拒绝虚无假设，接受研究假设


* 第二种方法：利用语法t-test进行计算
```
t-test groups = gender(1 2)
	/variables = income
	/criteria = ci(0.99).
```
	
	
* 第三种方法：采用拔靴法（从已有样本不断重复抽取1400个样本,包含男性和女性）修正抽样分布标准误后，再进行假设检验
* 注意：独立样本总体均值检验的拔靴法需要增加“input”，即分组变量
```
bootstrap 
	/variables target = income input = gender
	/criteria cilevel = 99 nsamples = 1400.
t-test groups = gender(1 2)
	/variables = income
	/criteria = ci(0.99).  
```
* 可知p值（=0.000714）小于0.01，拒绝虚无假设，接受研究假设.

#### 随堂练习：试检验2015年中国男性和女性的总体平均最高教育程度是否相等，置信区间为95%.
  
  
#### 7.基于给定摘要信息的独立样本总体均值检验
* 点选操作为：分析 -> 比较平均值 -> 摘要独立样本T检验 -> 分别填入样本1和样本2的个案数、均值、标准差及标签 -> 确定
* 语法结构为：spssinc summary ttest + n1 = 样本1个案数 + mean1 = 样本1均值 + sd1 = 样本1标准差 + label1 = 样本1标签 + （样本2信息）+ ci = 置信区间*100.

#### 案例（7-1）若样本1个数为500，均值为6.1，标准差为4.27，样本2个案数为500，均值为5.5，标准差为5.56。在95%置信区间下，判断两者总体均值是否相等。请写出虚无假设、研究假设和检验过程
* 虚无假设H0：样本1和样本2的总体均值相等
* 研究假设H1：样本1和样本2的总体均值不相等
* 第一种方法：根据统计公式直接进行计算
	* 此过程与第6节相同
* 第二种方法：利用语法spssinc summary ttest进行计算
```
spssinc summary ttest n1 = 500 mean1 = 6.1 sd1 = 4.27 label1 = "sample 1" 
                      n2 = 500 mean2 = 5.5 sd2 = 5.56 label2 = "sample 2" 
					  ci = 95.
```

---
date: "2021-08-01T22:05:05-05:00"
tags:
- 问卷录入
title: 第二节 问卷编码与数据录入（一）
---

#### 1. 新建语法（syntax）
* **快捷键操作：** Alt + F + N + S
* **点选操作：** 文件 -> 新建 -> 语法

![语法窗口](https://stat4soc.netlify.app/images/2.1.png)


#### 2. 语法的操作注意事项
* 执行语法可以通过两种方式
	- **快捷键操作：** 语法编辑器 -> 选中待执行的语法项 -> Ctrl + R
	- **点选操作：** 语法编辑器 -> 选中待执行的语法项 -> "运行特定项"（三角键）
* 语法项的符号一律使用半角符，即切换为输入法的英文输入方式
* 语法项的基本结构为：命令（command）+ 变量（variable） + 可选项（option） + 休止符"."
* 语法项中的命令和可选项不分英文大小写，变量分英文大小写
* 使用注释（comment）对语法项进行注解或做笔记，附上注释符号的内容不会被当成语法项来执行。注释有2种方式：
	- 第1种方式为"\*"到休止符"."。例如：
```
*以下语法为执行回归分析.
```
	- 第2种方式为"/*"到"*/"。例如：
```
/*以下语法为执行回归分析*/
``` 
* 同一段注释内不允许有空白行，否则空白行以下段落的注释在执行时报告错误
* 对于多段结构的语法项，建议第二至最后段首行均缩进1个制表符（轻敲键盘"tab"），以区别于其他语法项.
	- 注意：缩进风格（indent style）在不同程序语言的编撰文化中大相径庭
* 大体上，语法缩进应致力实现如下效果：
	- 视觉上容易分辨不同的语法项，突出重点，展示结构
	- 段落间既错落有致，又精简节约，减少不必要的留白
	- 语法项中对数据集和变量名等的名命名字内不可空格，词组使用半角符的下划线"_"连接
 	- 可以设置点选输出的结果是否附上语法
#### 案例（2-1）：给当前语法命名为“clds”
```
title "clds".
```
* 关闭结果输出语法
```
set printback = off.
```
* 开启结果输出语法
```
set printback = on.
```


#### 3. 设置工作目录(working directory)
* 设置工作目录可以让SPSS读取固定储存目录，简化导入、修改和存储数据步骤
#### 案例（3-1）：设置工作目录为“/Users/ginglam/Public/data”
```
cd "/Users/ginglam/Public/data".
```
* 显示当前工作目录
```
show directory.
```

![工作目录](https://stat4soc.netlify.app/images/2.2.png)


#### 4. 创建数据集（dataset）
* **点选操作：** 文件 -> 新建 -> 数据
* **语法操作：** new file.
```
new file.
```
 

#### 5. 读取已有数据集
* **点选操作：** 文件 -> 打开 -> 数据 -> 选择电脑中的数据
* **语法操作：** get file + "电脑储存路径"。注意，不同层级目录之间为斜杠"/".
* 如果数据类型为SPSS，格式为sav

#### 案例（5-1）：读取名为"clds2016_i"的SPSS数据
```
get file "clds2016_i.sav".
```
* 如果数据类型为Stata，格式为dta

![CLDS](https://stat4soc.netlify.app/images/2.3.png)

#### 案例（5-2）：读取名为"titanic"的Stata数据
```
get stata file "titanic.dta".
```

![titanic](https://stat4soc.netlify.app/images/2.4.png)

* 如果数据类型为Excel，格式为xls或xlsx，语法结构包括文件格式"/type"、路径"/file"、工作表"/sheet"、表内数据范围"/cellrange"以及是否指定第一行为变量名"/readnames"

#### 案例（5-3）：读取名为"auto"的Excel数据
```
get data
    /type = xls
    /file = "auto.xls"
    /sheet = name "domestic"
    /cellrange = full
    /readnames = on.
```

#### 6. 将新建空数据集或已读取的数据集，加以命名并置于窗口前
* 数据分析过程中需要导入不同数据集，每一次读取数据时须进行命名和激活
#### 案例（6-1）：将新导入的"clds2016_i"数据命名为"clds"
```
get file "clds2016_i.sav".
dataset name clds window = front.
```

![激活](https://stat4soc.netlify.app/images/2.5.png)

#### 案例（6-2）：激活名为"clds"的数据集
```
dataset activate clds.
```


#### 7. 创建或删除变量（variable）
* **点选操作：** 变量视图 -> "名称"列（双击空白处） -> 输入名称（建议用英文）-> 同行"类型"列（双击空白并点击"…"）-> 选择类型
* **语法操作：**
	- 若是从空数据集中创建变量，先激活数据集
	- 新增字符串型变量为：string + 变量名 + (a+取值宽度，适用于定类变量、定序变量)
	- 新增数字型变量为：numeric + 变量名 + (f+取值宽度，适用于定距变量、定比变量)
#### 案例（7-1）：创建定类变量gender（性别）和定序变量education（教育）
* 注意：变量命名必须为英文字符.
```
string gender (a2).
string education (a2).
```
* 等价于
```
string gender education(a2).
```
* 也等价于
```
string gender (a2) / 
       education (a3).
```

#### 案例（7-2）：创建定比变量income（收入）
```
numeric income (f8).
```
* 等价于
```
compute income = 0.
```

#### 案例（7-3）：删除变量gender、education和income
```
delete variables gender.
delete variables education.
delete variables income.
```
* 等价于
```
delete variables gender education income.
```
* 也等价于
```
delete variables education to income.
```

#### 8. 修改变量名称（variable names）
* **点选操作：** 变量视图 -> "名称"列（双击待修改变量） -> 输入新名称（建议用英文字符）
* **语法操作：** rename variables + (旧变量名 = 新变量名)
#### 案例（8-1）：将变量gender、education和income名称，分别修改为sex、educ和salary
```
rename variables (gender = sex).
rename variables (education = educ).
rename variables (income = salary).
```
* 等价于
```
rename variables (gender education income = sex educ salary).
```
* 也等价于
```
rename variables (gender to income = sex educ salary).
```
* 也等价于
```
rename variables (all = sex educ salary).
```

#### 9. 修改变量类型（types of variable）
* **点选操作：** 变量视图 -> "类型"列（单击待修改变量并点击"…"） -> 选择新类型
* **语法操作：** alter type + 变量名 + (变量类型简写+取值宽度)
* 注意：只有当数据视图存有数据时，才能对变量类型进行修改
#### 案例（9-1）：将变量gender、education和income的类型分别修改为
* gender -> 数字型
* education -> 数字型
* income -> 字符串型
```
alter type gender (f2).
alter type education (f2).
alter type income (a2).
```
* 等价于
```
alter type gender education (f2).
alter type income (a2).
```
#### 案例（9-2）：将所有变量类型均修改为字符串型
```	   
alter type gender to income (a2).
```
* 也等价于
```
alter type all (a2).
```

#### 10. 添加或修改变量标签（variable labels）
* 点选操作为：变量视图 -> "标签"列（双击待添加或待修改变量） -> 输入标签（中英文皆可）
* 语法结构为：variable labels + 变量名 + "变量标签（一般为问卷题目）"
#### 案例（10-1）：给变量gender、education和income添加变量标签
* gender -> "您的性别是？"
* education -> "您的教育程度是？"
* income -> "您的收入水平是？"
```
variable labels gender "您的性别是？".
variable labels education "您的教育程度是？".
variable labels income "您的收入水平是？".
```

![变量标签1](https://stat4soc.netlify.app/images/2.6.png)

* 同时添加或修改多个变量标签
* 语法结构为：variable labels + 变量名1 + "变量标签1" + / + 变量名2 + "变量标签2".
#### 案例（10-2）：修改变量gender、education和income的变量标签
* gender -> "受访者的性别是（访员观察后填写）？"
* education -> "您最终学历是什么？"
* income -> "您目前一个月挣多少钱？"
```
variable labels gender "受访者的性别是（访员观察后填写）？" /
                education "您最终学历是什么？" / 
	        income "您目前一个月挣多少钱？".
```

![变量标签2](https://stat4soc.netlify.app/images/2.7.png)

#### 11. 添加变量取值标签（value labels）
* 点选操作为：变量视图 -> "值"列（单击待添加变量并点击"…"） -> 输入值和标签（对应问卷选项号及选项内容） -> 添加 -> 确定
* 语法结构为：value labels + 变量名 + 取值 + "取值标签（一般为问卷题目的选项）"
#### 案例（11-1）：给变量education添加取值标签
* 当受访者回答"小学及以下" -> education=1
* 当受访者回答"初中" -> education=2
* 当受访者回答"高中" -> education=3
* 当受访者回答"大学及以上" -> education=4
```
value labels education 1 "小学及以下".
add value labels education 2 "初中".
add value labels education 3 "高中".
add value labels education 4 "大学及以上".
```
等价于
```
value labels education 1 "小学及以下" 2 "初中" 3 "高中" 4 "大学及以上".
```

![取值标签](https://stat4soc.netlify.app/images/2.8.png)

* 修改变量取值标签
* 点选操作为：变量视图 -> "值"列（单击待修改变量并点击"…"） -> 选择存储框中的值及标签 -> 输入新值或新标签 -> 修改 -> 确定
```
value labels education 1 "小学没毕业".
```

#### 案例（11-2）：同时给变量gender和education添加取值标签
```
value labels gender 0 "女性" 1 "男性" / 
             education 1 "小学及以下" 2 "初中" 3 "高中" 4 "大学及以上".
```

#### 12. 添加变量测量级别（measurement of variable）
* 点选操作为：变量视图 -> "测量"列（单击待添加变量） -> 出现下拉菜单后进行选择（定类变量选"名义"，定序变量选"有序"，定比变量选"标度"）
* 语法结构为：variable level + 变量名 + (nominal 或 ordinal 或 scale).
#### 案例（12-1）：同时给变量gender、education和income添加测量层次
* gender -> "名义"
* education -> "有序"
* income -> "标度"
```
variable level gender (nominal).
variable level education (ordinal).
variable level income (scale).
```
等价于
```
variable level gender (nominal) / 
               education (ordinal) / 
    	       income(scale).
```

![测量层次](https://stat4soc.netlify.app/images/2.9.png)

#### 随堂练习：将如下问卷题目录入SPSS
* 您当前的婚姻状况为？A 未婚 B 同居 C 在婚 D 离婚
* 您当前从事的行业为？A 服务业 B 制造和建造业 C 公共行政 D 农业
* 您每周累计工作多长时间？______（小时）
	* 提示:新建数据集，创建婚姻状况（marital）、工作行业（industry）和工作时间（working_time）三个变量
